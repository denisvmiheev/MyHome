#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Остаток кошелька.
// 
// Параметры:
//  Кошелек - СправочникСсылка.мх_КошелькиИСчета - Кошелек, по которому считаются остатки
//  Период - Дата - Дата остатков. Если не указано, то подставляется ТекущаяДата()
// 
// Возвращаемое значение:
//  Число - Остаток кошелька
Функция ОстатокКошелька(Кошелек, Период = Неопределено) Экспорт

	Если Период = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	Иначе
		ДатаСреза = Период;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Кошелек", Кошелек);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.мх_ДвиженияДенежныхСредств.Остатки(&ДатаСреза, Кошелек = &Кошелек) КАК ДвиженияДенежныхСредствОстатки";

	Выборка = Запрос.Выполнить().Выбрать();

	Баланс = 0;

	Если Выборка.Следующий() Тогда
		Баланс = Выборка.СуммаОстаток;
	КонецЕсли;

	Возврат Баланс;

КонецФункции

// Остатки кошельков.
// 
// Параметры:
//  Кошельки - Массив - Кошельки, по которым считается остаток
//  Период - Дата - Дата остатков. Если не указано, то подставляется ТекущаяДата()
//  ВСоответствие - Булево - Если ИСТИНА, то результат преобразуется в соответствие
// 
// Возвращаемое значение:
//  ТаблицаЗначений, Соответствие - Остатки кошельков
Функция ОстаткиКошельков(Кошельки, Период = Неопределено, ВСоответствие = Истина) Экспорт

	Если Период = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	Иначе
		ДатаСреза = Период;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Кошельки", Кошельки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.Кошелек,
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток КАК Баланс
	|ИЗ
	|	РегистрНакопления.мх_ДвиженияДенежныхСредств.Остатки(&ДатаСреза, Кошелек В (&Кошельки)) КАК
	|		ДвиженияДенежныхСредствОстатки
	|УПОРЯДОЧИТЬ ПО
	|	ДвиженияДенежныхСредствОстатки.Кошелек.Наименование УБЫВ";

	ТаблицаДанных = Запрос.Выполнить().Выгрузить();

	Результат = ТаблицаДанных;

	Если ВСоответствие Тогда

		Результат = Новый Соответствие;

		Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
			Результат.Вставить(СтрокаТаблицы.Кошелек, СтрокаТаблицы.Баланс);
		КонецЦикла;

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли