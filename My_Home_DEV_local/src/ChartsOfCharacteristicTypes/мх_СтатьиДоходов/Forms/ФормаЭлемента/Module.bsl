
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.ТипАналитикиПоУмолчанию <> Неопределено Тогда
		ТипАналитикиПоУмолчаниюПредставление = Строка(ТипЗнч(Объект.ТипАналитикиПоУмолчанию));
	КонецЕсли;
	
	ОбновитьСписокДоступныхТипов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипАналитикиПоУмолчаниюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ДоступныеТипы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ДоступныеТипы.Количество() = 1 Тогда
		ТипАналитикиПоУмолчаниюПредставление = Строка(ДоступныеТипы[0]);
		Возврат;	
	КонецЕсли;
	
	ВыбратьТипАналитикиПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	
	ОбновитьСписокДоступныхТипов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ВыбратьТипАналитикиПоУмолчанию()
	
	ВыбранноеЗначение = Ждать ДоступныеТипы.ВыбратьЭлементАсинх();
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;		
	КонецЕсли;
	
	ВыбранныйТип = ВыбранноеЗначение.Значение;
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ВыбранныйТип);
	
	ТипАналитикиПоУмолчаниюПредставление = Строка(ВыбранныйТип);
	
	ОписаниеТипа = Новый ОписаниеТипов(МассивТипов);
	Объект.ТипАналитикиПоУмолчанию = ОписаниеТипа.ПривестиЗначение(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДоступныхТипов()
	
	МассивТипов = Объект.ТипЗначения.Типы();
	
	ДоступныеТипы.ЗагрузитьЗначения(МассивТипов);
	
	Если МассивТипов.Количество() > 0
		И НЕ Объект.ТипЗначения.СодержитТип(ТипЗнч(Объект.ТипАналитикиПоУмолчанию)) Тогда
		ТипАналитикиПоУмолчаниюПредставление = Строка(МассивТипов[0]);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти





