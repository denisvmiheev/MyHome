
#Область ПрограммныйИнтерфейс

#Область Документы

Процедура ЗаполнитьРеквизитыДокументаПоШаблону(ДокументОбъект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьАвтораДокумента(ДокументОбъект);
	ЗаполнитьОтветственногоДокумента(ДокументОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти(ДокументОбъект, ИмяТабличнойЧасти, КолонкиГруппировок, КолонкиСуммирования) Экспорт
		
	МассивКолонокГруппировки = СтрРазделить(КолонкиГруппировок, ",");
	МассивКолонокСуммирования = СтрРазделить(КолонкиСуммирования, ",");
	
	ТабличнаяЧасть = ДокументОбъект[ИмяТабличнойЧасти];
	
	СтруктураЗаполнения = Новый Структура;
	
	Для каждого Колонка из МассивКолонокГруппировки Цикл		
		ИмяКолонки = СокрЛП(Колонка);		
		ДанныеКолонки = ТабличнаяЧасть.ВыгрузитьКолонку(ИмяКолонки);
		УникальныеДанныеКолонки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДанныеКолонки);
		Если УникальныеДанныеКолонки.Количество() = 1 Тогда
			СтруктураЗаполнения.Вставить(ИмяКолонки, УникальныеДанныеКолонки[0]);
		КонецЕсли;	
	КонецЦикла;
	
	Для каждого Колонка из МассивКолонокСуммирования Цикл
		ИмяКолонки = СокрЛП(Колонка);		
		ЗначениеИтога = ТабличнаяЧасть.Итог(ИмяКолонки);
		СтруктураЗаполнения.Вставить(ИмяКолонки, ЗначениеИтога);						
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ПВХ

// Заполнить тип аналитики по умолчанию.
// 
// Параметры:
//  ПВХОбъект - ПланВидовХарактеристикОбъект.мх_СтатьиДоходов, ПланВидовХарактеристикОбъект.мх_СтатьиРасходов
Процедура ЗаполнитьТипАналитикиПоУмолчанию(ПВХОбъект) Экспорт
	
	Если ПВХОбъект.ТипАналитикиПоУмолчанию = Неопределено Тогда
		
		ДоступныеТипы = ПВХОбъект.ТипЗначения.Типы();
		
		Если ДоступныеТипы.Количество() > 0 Тогда
			
			Массив = Новый Массив;
			Массив.Добавить(ДоступныеТипы[0]);
			
			ОписаниеТипов = Новый ОписаниеТипов(Массив);
			ПВХОбъект.ТипАналитикиПоУмолчанию = ОписаниеТипов.ПривестиЗначение();	
			
		КонецЕсли;
		
	КонецЕсли;			
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьАвтораДокумента(ДокументОбъект)
			
	Если ДокументОбъект.Метаданные().Реквизиты.Найти("Автор") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ДокументОбъект.ЭтоНовый() Тогда
			
		ДокументОбъект.Автор = Пользователи.ТекущийПользователь();			
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОтветственногоДокумента(ДокументОбъект)
			
	Если ДокументОбъект.Метаданные().Реквизиты.Найти("Ответственный") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ДокументОбъект.Ответственный) Тогда		
		ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();					
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти