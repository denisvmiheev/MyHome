
#Область ПрограммныйИнтерфейс

Функция ОстатокБюджетаНаДату(Знач ДатаПроверки = Неопределено) Экспорт
	
	ОстатокБюджета = 0;
	
	Если НЕ ЗначениеЗаполнено(ДатаПроверки) Тогда
		ДатаПроверки = ТекущаяДата();
	КонецЕсли;
	
	НачалоПериода = НачалоМесяца(ДатаПроверки);
	КонецПериода = КонецДня(ДатаПроверки);
	ГраницаКонецПериода = Новый Граница(КонецПериода, ВидГраницы.Включая);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", ГраницаКонецПериода);
	Запрос.УстановитьПараметр("КонецПериодаДата", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланированиеБюджетаОбороты.СуммаБюджетНаДеньПриход + ПланированиеБюджетаОбороты.СуммаРасход КАК Сумма
	|ИЗ
	|	РегистрНакопления.мх_ПланированиеБюджета.Обороты(&НачалоПериода, &КонецПериода,,) КАК ПланированиеБюджетаОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	-РасходыОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.мх_Расходы.Обороты(&НачалоПериода, &КонецПериода,,) КАК РасходыОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ISNULL(СУММА(мх_Расходы.Сумма), 0) КАК Сумма
	|ИЗ
	|	РегистрНакопления.мх_Расходы КАК мх_Расходы
	|ГДЕ
	|	мх_Расходы.Период МЕЖДУ &НачалоПериода И &КонецПериодаДата
	|	И мх_Расходы.ВнеБюджета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОстатокБюджета = ОстатокБюджета + Выборка.Сумма;
	КонецЦикла;
	
	Возврат ОстатокБюджета;
	
КонецФункции

#КонецОбласти