#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьНадписьМесяцаПланирования();
	РассчитатьКоличествоДнейПериода();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ПустаяСтрока(МесяцПланирования) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСТр("ru = '""Не указан месяц планирования""'"), , "МесяцПланирования");
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	СписокОшибок = Неопределено;
	ГруппаОшибок = "Объект.Расходы";
	ШаблонПоляОшибкиДаты = НСтр("ru = 'Объект.Расходы[%1].ДатаРасхода'");
	ШаблонТекстаОшибкиДаты = НСтр("ru = 'В строке %1 дата расхода находится вне периода планирования'");
	ТекстОднойОшибки = НСтр("ru = 'Дата расхода находится вне периода планирования'");
	
	Для каждого СтрокаРасхода Из Объект.Расходы Цикл
		
		Если СтрокаРасхода.ДатаРасхода > Объект.КонецПериода
			ИЛИ СтрокаРасхода.ДатаРасхода < Объект.НачалоПериода Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок, 
																	ШаблонПоляОшибкиДаты, 
																	ТекстОднойОшибки,
																	ГруппаОшибок,
																	СтрокаРасхода.НомерСтроки - 1,
																	ШаблонТекстаОшибкиДаты,
																	СтрокаРасхода.НомерСтроки - 1);				
				
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(СписокОшибок, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаНакопленияПриИзменении(Элемент)
	ПересчитатьБюджетНаДень();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПриИзменении(Элемент)
	ПересчитатьБюджетНаДень();
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриИзменении(Элемент)
	ПересчитатьБюджетНаДень();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСуммаПриИзменении(Элемент)
	ПересчитатьБюджетНаДень();
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаПриИзменении(Элемент)
	ПересчитатьБюджетНаДень();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВыбратьПериод(Команда)

	ПараметрыВыбораПериода = Новый Структура("НачалоПериода,КонецПериода,ВыборКварталов", Объект.НачалоПериода, Объект.КонецПериода, Ложь);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбораПериода, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт

	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(Объект, РезультатВыбора, "НачалоПериода,КонецПериода");
	УстановитьНадписьМесяцаПланирования();
	РассчитатьКоличествоДнейПериода();
	ПересчитатьБюджетНаДень();
	ПерезаполнитьДатыРасходов();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьМесяцаПланирования()

	Если ЗначениеЗаполнено(Объект.НачалоПериода) Тогда
		МесяцПланирования = Строка(Формат(Объект.НачалоПериода, "ДФ='MMMM yyyy'; ДЛФ='MMMM yyyy';"));
	Иначе
		МесяцПланирования = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКоличествоДнейПериода()
	ДнейВПериоде = (НачалоДня(Объект.КонецПериода) - НачалоДня(Объект.НачалоПериода)) / (60 * 60 * 24) + 1;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьБюджетНаДень()

	Если ДнейВПериоде = 0 Тогда
		Возврат;
	КонецЕсли;

	СуммаДоход = Объект.Доходы.Итог("Сумма");
	СуммаРасход = Объект.Расходы.Итог("Сумма");

	СвободныйОстаток = СуммаДоход - СуммаРасход - Объект.СуммаНакопления;

	Объект.СуммаБюджетНаДень = СвободныйОстаток / ДнейВПериоде;

КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьДатыРасходов()
	
	Для каждого СтрокаРасхода Из Объект.Расходы Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаРасхода.ДатаРасхода) Тогда
			СтрокаРасхода.ДатаРасхода = Объект.НачалоПериода;
			Продолжить;
		КонецЕсли;
		
		ДеньРасхода = День(СтрокаРасхода.ДатаРасхода) - 1;		
		НоваяДата = Объект.НачалоПериода + ДеньРасхода * 86400;
		ДатаРасхода = Мин(НоваяДата, Объект.КонецПериода);
		СтрокаРасхода.ДатаРасхода = ДатаРасхода;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти