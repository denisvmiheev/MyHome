#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	мх_МобильныйИнтерфейс.ОбработкаПолученияФормы(ВидФормы,
												Параметры,
											 	ВыбраннаяФорма,
											 	ДополнительнаяИнформация,
											 	СтандартнаяОбработка,
											 	"Документ.мх_ПеремещениеДенежныхСредств");	

КонецПроцедуры

#КонецОбласти	

#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПеремещений.Ссылка КАК Регистратор,
	|	ТаблицаПеремещений.ДатаОперации КАК Период,
	|	ТаблицаПеремещений.НомерСтроки,
	|	ТаблицаПеремещений.КошелекОткуда КАК Кошелек,
	|	ТаблицаПеремещений.Сумма,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ИЗ
	|	Документ.мх_ПеремещениеДенежныхСредств.Перемещения КАК ТаблицаПеремещений
	|ГДЕ
	|	ТаблицаПеремещений.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПеремещений.Ссылка КАК Регистратор,
	|	ТаблицаПеремещений.ДатаОперации КАК Период,
	|	ТаблицаПеремещений.НомерСтроки,
	|	ТаблицаПеремещений.КошелекКуда КАК КошелекОткуда,
	|	ТаблицаПеремещений.Сумма,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
	|ИЗ
	|	Документ.мх_ПеремещениеДенежныхСредств.Перемещения КАК ТаблицаПеремещений
	|ГДЕ
	|	ТаблицаПеремещений.Ссылка = &Ссылка";

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаДвиженияДенежныхСредств", РезультатЗапроса[0].Выгрузить());

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияДенежныхСредств(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДвиженияДенежныхСредств;

	Если Отказ Или ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДенежныхСредств = Документ.Движения.мх_ДвиженияДенежныхСредств;
	ДвиженияДенежныхСредств.Записывать = Истина;
	ДвиженияДенежныхСредств.Загрузить(ТаблицаДвижений);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли