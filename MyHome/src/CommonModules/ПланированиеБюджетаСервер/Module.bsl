
#Область ПрограммныйИнтерфейс

Функция ОстатокБюджетаНаДату(Знач ДатаПроверки = Неопределено) Экспорт
	
	ОстатокБюджета = 0;
	
	Если НЕ ЗначениеЗаполнено(ДатаПроверки) Тогда
		ДатаПроверки = ТекущаяДата();
	КонецЕсли;
	
	НачалоПериода = НачалоМесяца(ДатаПроверки);
	КонецПериода = КонецДня(ДатаПроверки);
	ГраницаКонецПериода = Новый Граница(КонецПериода, ВидГраницы.Включая);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", ГраницаКонецПериода);
	Запрос.УстановитьПараметр("КонецПериодаДата", КонецПериода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланированиеБюджетаОбороты.СуммаБюджетНаДеньПриход + ПланированиеБюджетаОбороты.СуммаРасход КАК Сумма
	|ИЗ
	|	РегистрНакопления.мх_ПланированиеБюджета.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ПланированиеБюджетаОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	-РасходыОбороты.СуммаОборот * ЕСТЬNULL(КурсыВалют.Курс, 1)
	|ИЗ
	|	РегистрНакопления.мх_Расходы.Обороты(&НачалоПериода, &КонецПериода, , ) КАК РасходыОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсыВалют
	|		ПО РасходыОбороты.Валюта = КурсыВалют.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(мх_Расходы.Сумма), 0) * ЕСТЬNULL(КурсыВалют.Курс, 1)
	|ИЗ
	|	РегистрНакопления.мх_Расходы КАК мх_Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсыВалют
	|		ПО мх_Расходы.Валюта = КурсыВалют.Валюта
	|ГДЕ
	|	мх_Расходы.Период МЕЖДУ &НачалоПериода И &КонецПериодаДата
	|	И мх_Расходы.ВнеБюджета
	|
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалют.Курс";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОстатокБюджета = ОстатокБюджета + Выборка.Сумма;
	КонецЦикла;
	
	Возврат ОстатокБюджета;
	
КонецФункции

#КонецОбласти