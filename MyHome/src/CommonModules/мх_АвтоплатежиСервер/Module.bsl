#Область ПрограммныйИнтерфейс

Функция ВыполнитьОбработкуАвтоплатежа(Автоплатеж) Экспорт

	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");

	Реквизиты = "ВидПлатежа,Сумма,СтатьяДоходовРасходов,Кошелек,АналитикаДоходов,АналитикаРасходов";
	РеквизитыПлатежа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Автоплатеж, Реквизиты);

	Если РеквизитыПлатежа.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыДвиженийДенежныхСредств.Расход") Тогда
		Результат = СоздатьРасход(РеквизитыПлатежа);
	ИначеЕсли РеквизитыПлатежа.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыДвиженийДенежныхСредств.Доход") Тогда
		Результат = СоздатьДоход(РеквизитыПлатежа);
	Иначе
		ВызватьИсключение НСтр("ru = 'Необрабатываемый формат платежа'");
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьРасписаниеАвтоплатежа(Знач ИдентификаторРегламентногоЗадания) Экспорт
	
	Возврат мх_РегламентныеЗадания.ПолучитьРасписаниеРегламентногоЗадания(ИдентификаторРегламентногоЗадания, "мх_ОбработкаАвтоплатежей");

КонецФункции

Процедура УстановитьРасписаниеАвтоплатежа(Знач АвтоплатежОбъект, Расписание, Использование) Экспорт
	
	мх_РегламентныеЗадания.УстановитьРасписаниеРегламентногоЗадания(АвтоплатежОбъект,
																 "мх_ОбработкаАвтоплатежей",
																 Расписание,
																 Использование);

КонецПроцедуры

Процедура ВыполнитьОбработкуАвтоплатежей(ИдентификаторыПлатежей) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторыПлатежей", ИдентификаторыПлатежей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Автоплатежи.Ссылка,
	|	Автоплатежи.ПометкаУдаления,
	|	Автоплатежи.Используется
	|ИЗ
	|	Справочник.мх_Автоплатежи КАК Автоплатежи
	|ГДЕ
	|	Автоплатежи.ИдентификаторРегламентногоЗадания В (&ИдентификаторыПлатежей)";

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда

		Если Не Выборка.Используется Или Выборка.ПометкаУдаления Тогда
			ВызватьИсключение НСтр("ru = 'Автоплатеж не используется'");
		КонецЕсли;

		Результат = ВыполнитьОбработкуАвтоплатежа(Выборка.Ссылка);

		Если Не Результат.Успех Тогда
			ВызватьИсключение Результат.ОписаниеОшибки;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция СоздатьРасход(Знач РеквизитыПлатежа)

	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");

	ПараметрыРасхода = РеквизитыПлатежа;
	ПараметрыРасхода.Вставить("СтатьяРасходов", РеквизитыПлатежа.СтатьяДоходовРасходов);
	ПараметрыРасхода.Вставить("АналитикаРасходов", РеквизитыПлатежа.АналитикаРасходов);
	ПараметрыРасхода.Вставить("Комментарий", "Создан автоматически");

	Попытка
		Результат = Документы.мх_РегистрацияРасходов.ДобавитьРасход(ПараметрыРасхода);
	Исключение
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;

	Возврат Результат;

КонецФункции

Функция СоздатьДоход(Знач РеквизитыПлатежа)

	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");

	ПараметрыДохода = РеквизитыПлатежа;
	ПараметрыДохода.Вставить("СтатьяДоходов", РеквизитыПлатежа.СтатьяДоходовРасходов);
	ПараметрыДохода.Вставить("АналитикаДоходов", РеквизитыПлатежа.АналитикаДоходов);
	ПараметрыДохода.Вставить("Комментарий", "Создан автоматически");

	Попытка
		Результат = Документы.мх_РегистрацияДоходов.ДобавитьДоход(ПараметрыДохода);
	Исключение
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;

	Возврат Результат;

КонецФункции
#КонецОбласти