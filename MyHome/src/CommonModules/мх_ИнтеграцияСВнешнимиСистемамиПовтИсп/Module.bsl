#Область ПрограммныйИнтерфейс

// Возвращает данные внешней системы для проверки чеков
// Если система не указана в настройках, то генерируется исключение
// 
// Возвращаемое значение:
// 	Структура - см. возвращаемое значение процедуры "ДанныеВнешнейСистемы"
Функция ДанныеСистемыПроверкиЧеков() Экспорт

	Система = ИспользуемаяСистемаПроверкиЧеков();

	Если Не ЗначениеЗаполнено(Система) Тогда
		ТекстИсключения = НСтр("ru = 'В настройках программы не указана используемая система для проверки чеков'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ПолучаемыеДанные = "Сервер,Порт,АдресРесурса,ТокенДоступа,Пользователь,Пароль,ЗащищенноеСоединение";

	ДанныеСистемы = ДанныеВнешнейСистемы(Система, ПолучаемыеДанные);

	Возврат ДанныеСистемы;

КонецФункции

// Возвращает данные внешней системы
// 
// Параметры:
// 	ВнешняяСистема - СправочникСсылка.мх_ВнешниеСистемы - Внешняя система
// 	ПодставлятьНеопределено - Булево - Если Истина, то каждое 
// 	незаполненное значение будет заменено на "Неопределено"
// Возвращаемое значение:
// 	Структура - данные внешней системы
Функция ДанныеВнешнейСистемы(ВнешняяСистема, ПолучаемыеДанные, ПодставлятьНеопределено = Истина) Экспорт

	//СтрокаРеквизитов = "Сервер,Порт,АдресРесурса,ТокенДоступа,Пользователь,Пароль,ЗащищенноеСоединение";

	ПолучаемыеДанные = СтрРазделить(ПолучаемыеДанные, ",", Ложь);
	Для Индекс = 0 По ПолучаемыеДанные.ВГраница() Цикл
		ПолучаемыеДанные[Индекс] = СокрЛП(ПолучаемыеДанные[Индекс]);
	КонецЦикла;
	
	ДанныеДляПолученияИзБезопасногоХранилища = ДанныеДляПолученияИзБезопасногоХранилища();
	
	Для каждого Имя из ДанныеДляПолученияИзБезопасногоХранилища Цикл
		
		ИндексЭлемента = ПолучаемыеДанные.Найти(Имя);
		
		Если ИндексЭлемента <> Неопределено Тогда
			ПолучаемыеДанные.Удалить(ИндексЭлемента);
		КонецЕсли;
		
	КонецЦикла;
	
	ПолучаемыеДанные = СтрСоединить(ПолучаемыеДанные, ",");

	ЗначенияНастроек = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВнешняяСистема, ПолучаемыеДанные);
	
	Если ДанныеДляПолученияИзБезопасногоХранилища.Количество() > 0 Тогда
		Ключи = СтрСоединить(ДанныеДляПолученияИзБезопасногоХранилища, ",");
		ДанныеБезопасногоХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ВнешняяСистема, Ключи);
		
		Если ТипЗнч(ДанныеБезопасногоХранилища) = Тип("Структура") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ЗначенияНастроек, ДанныеБезопасногоХранилища);
		ИначеЕсли ТипЗнч(ДанныеБезопасногоХранилища) = Неопределено Тогда
		Иначе
			ЗначенияНастроек.Вставить(ДанныеДляПолученияИзБезопасногоХранилища[0], ДанныеБезопасногоХранилища);
		КонецЕсли;
	КонецЕсли;
	
	Если ПодставлятьНеопределено Тогда
		Для каждого Настройка из ЗначенияНастроек Цикл
			Если НЕ ЗначениеЗаполнено(Настройка.Значение) Тогда
				Настройка.Значение = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЗначенияНастроек;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИспользуемаяСистемаПроверкиЧеков()

	УстановитьПривилегированныйРежим(Истина);
	Система = Константы.мх_СистемаПроверкиЧеков.Получить();
	УстановитьПривилегированныйРежим(Ложь);

	Возврат Система;

КонецФункции

Функция ДанныеДляПолученияИзБезопасногоХранилища()
	
	Имена = Новый Массив;
	Имена.Добавить("Пароль");

	Возврат Имена;
	
КонецФункции

#КонецОбласти