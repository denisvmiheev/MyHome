
#Область ПрограммныйИнтерфейс

Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	Описание.Имя = "MyHome";
	Описание.Версия = "0.1.0.2";
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
	Описание.ЭтоКонфигурация = Ложь;
КонецПроцедуры

Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик					 = Обработчики.Добавить();
	Обработчик.Версия			 = "0.0.3.0";
	Обработчик.Процедура		 = "мх_ОбновлениеИнформационнойБазы.ПерейтиНаВерсию_0_0_3_0";
	Обработчик.РежимВыполнения	 = "Монопольно";
	
	Обработчик					 = Обработчики.Добавить();
	Обработчик.Версия			 = "0.0.3.1";
	Обработчик.Процедура		 = "мх_ОбновлениеИнформационнойБазы.ПерейтиНаВерсию_0_0_3_1";
	Обработчик.РежимВыполнения	 = "Монопольно";
	
	Обработчик					 = Обработчики.Добавить();
	Обработчик.Версия			 = "0.1.0.2";
	Обработчик.Процедура		 = "мх_ОбновлениеИнформационнойБазы.ПерейтиНаВерсию_0_1_0_2";
	Обработчик.РежимВыполнения	 = "Монопольно";
	
КонецПроцедуры

Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
КонецПроцедуры

Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
	
	Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	
КонецПроцедуры

Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	
КонецПроцедуры

Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
	
КонецПроцедуры

Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации, Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ПерейтиНаВерсию_0_0_3_0() Экспорт
	
	ПерезаполнитьРеквизитыПрисоединенныхФайлов();
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_0_0_3_1() Экспорт
	
	ПерезаполнитьТипыАналитикПоУмолчаниюДляСтатейДоходовРасходов();
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_0_1_0_2() Экспорт
	
	ПерезаполнитьРеквизитыШапкиДокументовПоДаннымТабличныхЧастей();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерезаполнитьРеквизитыПрисоединенныхФайлов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГарантияПрисоединенныеФайлы.Ссылка КАК СсылкаНаФайл
	|ИЗ
	|	Справочник.мх_ГарантияПрисоединенныеФайлы КАК ГарантияПрисоединенныеФайлы
	|ГДЕ
	|	ГарантияПрисоединенныеФайлы.ДатаСоздания = ДАТАВРЕМЯ(1, 1, 1)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДатаПереноса = ТекущаяДата();
	ТипХранения = ПредопределенноеЗначение("Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе");
	Автор = Пользователи.ТекущийПользователь();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			ФайлОбъект								 = Выборка.СсылкаНаФайл.ПолучитьОбъект();
			ФайлОбъект.ОбменДанными.Загрузка		 = Истина;
			ФайлОбъект.Автор						 = Автор;
			ФайлОбъект.ДатаСоздания					 = ДатаПереноса;
			ФайлОбъект.ТипХраненияФайла				 = ТипХранения;
			ФайлОбъект.ДатаМодификацииУниверсальная	 = ДатаПереноса;

			ДанныеХранилища							 = ФайлОбъект.Удалить_ХранилищеФайла.Получить();
			ФайлОбъект.ФайлХранилище				 = Новый ХранилищеЗначения(ДанныеХранилища);

			ФайлОбъект.Расширение					 = СтрЗаменить(ФайлОбъект.Удалить_Расширение, ".", "");
			ФайлОбъект.ИндексКартинки				 = РаботаСФайламиСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(ФайлОбъект.Расширение);
			ФайлОбъект.Записать();

			МенеджерЗаписи						 = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Файл					 = ФайлОбъект.Ссылка;
			МенеджерЗаписи.ДвоичныеДанныеФайла	 = Новый ХранилищеЗначения(ДанныеХранилища);
			МенеджерЗаписи.Записать();

			НаличиеФайлов						 = РегистрыСведений.НаличиеФайлов.СоздатьМенеджерЗаписи();
			НаличиеФайлов.ОбъектСФайлами		 = ФайлОбъект.ВладелецФайла;
			НаличиеФайлов.ЕстьФайлы				 = Истина;
			НаличиеФайлов.Записать();		
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерезаполнитьТипыАналитикПоУмолчаниюДляСтатейДоходовРасходов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.мх_СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.ТипАналитикиПоУмолчанию = НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиДоходов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.мх_СтатьиДоходов КАК СтатьиДоходов
	|ГДЕ
	|	СтатьиДоходов.ТипАналитикиПоУмолчанию = НЕОПРЕДЕЛЕНО";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ОбменДанными.Загрузка = Истина;
		мх_ОбработчикиСобытий.ЗаполнитьТипАналитикиПоУмолчанию(Объект);
		Объект.Записать();
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПерезаполнитьРеквизитыШапкиДокументовПоДаннымТабличныхЧастей()
	
	ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти("мх_ВозвратРасходов",
			"Расходы",
			"Кошелек, СтатьяРасходов, АналитикаРасходов, ДатаОперации, ВнеБюджета",
			"Сумма");	
			
	ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти("мх_ПеремещениеДенежныхСредств",
			"Перемещения",
			"КошелекОткуда, КошелекКуда",
			"Сумма");
			
	ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти("мх_РегистрацияДоходов",
			"Доходы",
			"Кошелек, СтатьяДоходов, АналитикаДоходов, ДатаОперации, ВнеплановыйДоход",
			"Сумма");
			
	ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти("мх_РегистрацияРасходов",
			"Расходы",
			"Кошелек, СтатьяРасходов, АналитикаРасходов, ДатаОперации, ВнеБюджета",
			"Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти(ИмяДокумента, ИмяТабличнойЧасти, КолонкиГруппировок, КолонкиСуммирования)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка
	|ИЗ
	|	&ИмяТаблицыДокументов КАК ТаблицаДокументов";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицыДокументов", "Документ." + ИмяДокумента);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		мх_ОбработчикиСобытий.ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти(ДокументОбъект, ИмяТабличнойЧасти, КолонкиГруппировок, КолонкиСуммирования);
		ДокументОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
