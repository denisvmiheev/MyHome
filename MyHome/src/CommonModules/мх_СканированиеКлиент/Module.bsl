#Область ПрограммныйИнтерфейс

Процедура СканироватьШтрихкод(Оповещение) Экспорт

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда
	Если Не СредстваМультимедиа.ПоддерживаетсяСканированиеШтрихКодов() Тогда
		ТекстСообщения = НСтр("ru = 'Сканирование штрихкодов не поддерживается'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	ДопПараметры = Новый Структура("Оповещение", Оповещение);

	ОбработчикСканирования = Новый ОписаниеОповещения("ОбработкаСканирования", мх_СканированиеКлиент, ДопПараметры);
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ОбработкаЗакрытияИнтерфейса", мх_СканированиеКлиент, ДопПараметры);

	СредстваМультимедиа.ПоказатьСканированиеШтрихКодов("Наведите камеру на штрихкод", ОбработчикСканирования, ОбработчикЗакрытия,
		ТипШтрихКода.Все);

#Иначе
#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаЗакрытияИнтерфейса(ДополнительныеПараметры) Экспорт

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда
	СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
#КонецЕсли

КонецПроцедуры

Процедура ОбработкаСканирования(Штрихкод, Результат, Сообщение, ДополнительныеПараметры) Экспорт

	РезультатСканирования = Новый Структура("Успех,Штрихкод", Истина, "");

	Если Результат Тогда
		РезультатСканирования.Штрихкод = Штрихкод;
	Иначе
		РезультатСканирования.Успех = Ложь;
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, РезультатСканирования);

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда
	СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
#КонецЕсли

КонецПроцедуры

#КонецОбласти