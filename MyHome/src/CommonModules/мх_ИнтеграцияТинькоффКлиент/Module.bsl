
#Область ПрограммныйИнтерфейс

// Выполнить авторизацию в личном кабинете.
// 
// Параметры:
//  Настройка - СправочникСсылка.мх_ВнешниеСистемы - Настройка подключения к ЛК
// 
// Возвращаемое значение:
//  Структура - Результат авторизации
Асинх Функция ВыполнитьАвторизациюВЛичномКабинете(Настройка) Экспорт
	
	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");
	
	ПараметрыПодключения = мх_ИнтеграцияТинькоффВызовСервера.ПараметрыПодключенияКЛичномуКабинету(Настройка);
	Заголовки = мх_ИнтеграцияТинькоффКлиентСервер.ОбязательныеЗаголовкиЗапроса();
	
	РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("session",
																			,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			,
																			"POST");
																			
	Если РезультатЗапроса.Статус <> Истина Тогда
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = РезультатЗапроса.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;

	sessionid = РезультатЗапроса.ДанныеОтвета["payload"];
	мх_ИнтеграцияТинькоффВызовСервера.УстановитьЗначениеНастройкиВнешнейСистемы(Настройка, "Тинькофф_ИдентификаторСессии", sessionid);    
		
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("sessionid", sessionid);         	
	ПараметрыЗапроса.Вставить("wuid", ПараметрыПодключения.Тинькофф_wuid);
	
	ТелоЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ТелоЗапросаПриАвторизацииПослеВводаТелефона(ПараметрыПодключения);
	
	РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("sign_up",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			ТелоЗапроса,
																			"POST");
																			
	ДанныеОтвета = РезультатЗапроса.ДанныеОтвета;
	КодРезультата = ДанныеОтвета["resultCode"];
	
	ТекстОшибки = ДанныеОтвета["errorMessage"];
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = ТекстОшибки;
		Возврат Результат;
	КонецЕсли;
	
	Если КодРезультата = "WAITING_CONFIRMATION" Тогда
		
		СмсКод = Ждать ВвестиЗначениеАсинх("", "Введите код из СМС");
		Если СмсКод = Неопределено Тогда
			Результат.Успех = Ложь;
			Результат.ОписаниеОшибки(НСтр("ru = 'Пользователь отказался от ввода СМС-кода'"));
			Возврат Результат;
		КонецЕсли;
		
		ШаблонКодаПодтверждения = "%7B%22SMSBYID%22%3A%22&КодПодтверждения%22%7D";
		КодПодтверждения = СтрЗаменить(ШаблонКодаПодтверждения, "&КодПодтверждения", СмсКод);
		
		ПараметрыТела = Новый Соответствие;     
		ПараметрыТела.Вставить("confirmationData", КодПодтверждения);                                         
		ПараметрыТела.Вставить("initialOperation", "sign_up");
		ПараметрыТела.Вставить("initialOperationTicket", ДанныеОтвета["operationTicket"]);
		
		ТелоЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.СформироватьСтрокуПараметров(ПараметрыТела);   
		РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("confirm",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			ТелоЗапроса,
																			"POST");
																			
		КодРезультата = РезультатЗапроса.ДанныеОтвета["resultCode"];	
	
		Если КодРезультата = "OK" Тогда 
		
			ТелоЗапроса = мх_ИнтеграцияТинькоффКлиентСерверПовтИсп.ТелоЗапросаАвторизацииПослеВводаПароля(ПараметрыЗапроса.wuid,
																											ПараметрыПодключения.Пароль);
				
			РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("confirm",
																				ПараметрыЗапроса,
																				ПараметрыПодключения.Сервер,
																				Заголовки,
																				ТелоЗапроса,
																				"POST");

			РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("confirm",
																				ПараметрыЗапроса,
																				ПараметрыПодключения.Сервер,
																				Заголовки,
																				ТелоЗапроса,
																				"POST");

		Иначе
			
			Результат.Успех = Ложь;
			Результат.ОписаниеОшибки(НСтр("ru = 'Произошла ошибка при попытке входа'"));
			Возврат Результат;
			
		КонецЕсли;
		
		
	Иначе
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Получен необрабатываемый код результата запроса %1'"), КодРезультата);
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьВходВЛичныйКабинет(Настройка) Экспорт
	
	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");
	
	ПараметрыПодключения = мх_ИнтеграцияТинькоффВызовСервера.ПараметрыПодключенияКЛичномуКабинету(Настройка);
	Заголовки = мх_ИнтеграцияТинькоффКлиентСервер.ЗаголовкиПриВыполненииВхода(ПараметрыПодключения);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("sessionid", ПараметрыПодключения.Тинькофф_ИдентификаторСессии);
	
	РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("session_status",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			,
																			"GET");
																			
	Если РезультатЗапроса.Статус <> Истина Тогда
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = РезультатЗапроса.СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;	
		
	ОбработатьРезультатЗапросаПриВыполненииВхода(РезультатЗапроса, Результат, ПараметрыПодключения, Заголовки);
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьРезультатЗапросаПриВыполненииВхода(Знач РезультатЗапроса, Результат, ПараметрыПодключения, Заголовки)
	
	ДанныеОтвета = РезультатЗапроса.ДанныеОтвета;
	КодОтвета = ДанныеОтвета["resultCode"];
	
	Если КодОтвета = "SESSION_IS_ABSENT" Тогда
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("sessionid", ПараметрыПодключения.Тинькофф_ИдентификаторСессии);
		ПараметрыЗапроса.Вставить("wuid", ПараметрыПодключения.Тинькофф_wuid);
		
		РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("session",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			,
																			"GET");
																			
		sessionid = РезультатЗапроса.ДанныеОтвета["payload"];
		мх_ИнтеграцияТинькоффВызовСервера.УстановитьЗначениеНастройкиВнешнейСистемы(ПараметрыПодключения.Ссылка,
																					 "Тинькофф_ИдентификаторСессии",
																					 sessionid);    
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("sessionid", sessionid); 
		
		РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("session_status",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			,
																			"GET");
																			
													
		Если РезультатЗапроса.Статус <> Истина Тогда
			Результат.Успех = Ложь;
			Результат.ОписаниеОшибки = РезультатЗапроса.СообщениеОбОшибке;
			Возврат;
		КонецЕсли;	
	
		ДанныеОтвета = РезультатЗапроса.ДанныеОтвета;
		КодОтвета = ДанныеОтвета["resultCode"];
		
		ОбработатьРезультатЗапросаПриВыполненииВхода(РезультатЗапроса, Результат, ПараметрыПодключения, Заголовки); 
		
	ИначеЕсли КодОтвета = "OK" Тогда
		   	
		ИнформацияОСессии = ДанныеОтвета["payload"];
		
		УровеньДоступа = ИнформацияОСессии["accessLevel"];
		
		Если УровеньДоступа = "ANONYMOUS" Тогда
			
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("sessionid", ПараметрыПодключения.Тинькофф_ИдентификаторСессии);
			ПараметрыЗапроса.Вставить("wuid", ПараметрыПодключения.Тинькофф_wuid);
			
			ТелоЗапроса = мх_ИнтеграцияТинькоффКлиентСерверПовтИсп.ТелоЗапросаАвторизацииПослеВводаПароля(ПараметрыЗапроса.wuid,
																										 ПараметрыПодключения.Пароль);	
																										 
			РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("sign_up",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			ТелоЗапроса);
																			
			ОбработатьРезультатЗапросаПриВыполненииВхода(РезультатЗапроса, Результат, ПараметрыПодключения, Заголовки);
			
		ИначеЕсли УровеньДоступа = "CANDIDATE" Тогда
		
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("sessionid", ПараметрыПодключения.Тинькофф_ИдентификаторСессии);
			ПараметрыЗапроса.Вставить("wuid", ПараметрыПодключения.Тинькофф_wuid);
			
			ТелоЗапроса = мх_ИнтеграцияТинькоффКлиентСерверПовтИсп.ТелоЗапросаАвторизацииПослеВводаПароля(ПараметрыЗапроса.wuid,
																										 ПараметрыПодключения.Пароль);	
																										 
			РезультатЗапроса = мх_ИнтеграцияТинькоффКлиентСервер.ОтправитьЗапросАпи("level_up",
																			ПараметрыЗапроса,
																			ПараметрыПодключения.Сервер,
																			Заголовки,
																			ТелоЗапроса);
																			
			ОбработатьРезультатЗапросаПриВыполненииВхода(РезультатЗапроса, Результат, ПараметрыПодключения, Заголовки);
			
		ИначеЕсли УровеньДоступа = "CLIENT" Тогда
					
			Возврат;
			
		КонецЕсли;			
		
	Иначе
		
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Необрабатываемый код ответа %1'"), КодОтвета);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти