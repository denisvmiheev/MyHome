
#Область ПрограммныйИнтерфейс

Процедура УстановитьРасписаниеРегламентногоЗадания(Знач СвязанныйОбъект, РегламентноеЗадание, Расписание, Использование) Экспорт

	СсылкаНаОбъект = СвязанныйОбъект.Ссылка;

	Если ДополнительныеОтчетыИОбработкиКлиентСервер.РасписаниеЗадано(Расписание) Тогда

		Если ЗначениеЗаполнено(СвязанныйОбъект.ИдентификаторРегламентногоЗадания) Тогда
			Задание = РегламентныеЗаданияСервер.Задание(СвязанныйОбъект.ИдентификаторРегламентногоЗадания);
		КонецЕсли;
		
		МетаданныеЗадания = Метаданные.РегламентныеЗадания[РегламентноеЗадание];

		Если Задание = Неопределено Тогда

			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Метаданные", МетаданныеЗадания);

			Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
			СвязанныйОбъект.ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;

		КонецЕсли;

		ИзменениеЗадания = Новый Структура;
		ИзменениеЗадания.Вставить("Расписание", Расписание);
		ИзменениеЗадания.Вставить("Наименование", ПредставлениеЗадания(СсылкаНаОбъект, МетаданныеЗадания));
		ИзменениеЗадания.Вставить("Использование", Использование);
		
		//TODO Добавить возможность указывать пользователя в рег. задании автоплатежа	
		//Если Не ПустаяСтрока(ТипОчередиОбъект.ИмяПользователя) Тогда 
		//	ИзменениеЗадания.Вставить("ИмяПользователя", ТипОчередиОбъект.ИмяПользователя);
		//КонецЕсли;

		ПараметрыПроцедуры = Новый Массив;
		ПараметрыПроцедуры.Добавить(СвязанныйОбъект.ИдентификаторРегламентногоЗадания);
		ИзменениеЗадания.Вставить("Параметры", ПараметрыПроцедуры);

		РегламентныеЗаданияСервер.ИзменитьЗадание(СвязанныйОбъект.ИдентификаторРегламентногоЗадания, ИзменениеЗадания);

	Иначе

		Если ЗначениеЗаполнено(СвязанныйОбъект.ИдентификаторРегламентногоЗадания) Тогда

			Задание = РегламентныеЗаданияСервер.Задание(СвязанныйОбъект.ИдентификаторРегламентногоЗадания);
			Если Задание <> Неопределено Тогда
				РегламентныеЗаданияСервер.УдалитьЗадание(СвязанныйОбъект.ИдентификаторРегламентногоЗадания);
			КонецЕсли;

			СвязанныйОбъект.ИдентификаторРегламентногоЗадания = Неопределено;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьРасписаниеРегламентногоЗадания(Знач ИдентификаторРегламентногоЗадания, РегламентноеЗадание) Экспорт
	
	Результат = Новый Структура("Расписание, Использование", Новый РасписаниеРегламентногоЗадания, Ложь);

	Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("УникальныйИдентификатор", ИдентификаторРегламентногоЗадания);
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания[РегламентноеЗадание]);
		МассивЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
		Если МассивЗаданий.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат, МассивЗаданий[0], "Расписание, Использование");
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеЗадания(СсылкаНаОбъект, РегламентноеЗадание)
	
	ШаблонНаименования = "%1: %2";
	Наименование = Лев(СтрШаблон(ШаблонНаименования, РегламентноеЗадание.Синоним, СсылкаНаОбъект), 120);
	
	Возврат Наименование;
	
КонецФункции

#КонецОбласти