
#Область ПрограммныйИнтерфейс

Функция ТипыКошельковСобственныхСредств() Экспорт
	
	Возврат Перечисления.мх_ТипыКошельковИСчетов.ТипыКошельковСобственныхСредств();
	
КонецФункции

Функция ТипыКошельковЗаемныхСредств() Экспорт
	
	Возврат Перечисления.мх_ТипыКошельковИСчетов.ТипыКошельковЗаемныхСредств();
	
КонецФункции

// Используемые кошельки по типу.
// 
// Параметры:
//  ТипыКошельков - ПеречислениеСсылка.мх_ТипыКошельковИСчетов - Типы кошельков
// 
// Возвращаемое значение:
//  Массив - Используемые кошельки по типу
Функция ИспользуемыеКошелькиПоТипу(ТипыКошельков) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Типы", ТипыКошельков);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КошелькиИСчета.Ссылка
	|ИЗ
	|	Справочник.мх_КошелькиИСчета КАК КошелькиИСчета
	|ГДЕ
	|	КошелькиИСчета.ТипСчета В (&Типы)
	|	И КошелькиИСчета.Используется";
	
	МассивКошельков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивКошельков;
	
КонецФункции

Функция ВалютаПоУмолчанию() Экспорт
	Возврат мх_КошелькиИСчетаПовтИсп.ВалютаПоУмолчанию();
КонецФункции

Функция ВалютаКошелька(Знач Кошелек) Экспорт
	Возврат мх_КошелькиИСчетаПовтИсп.ВалютаКошелька(Кошелек);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьВалютуПоУмолчанию() Экспорт
	
	КлассификаторВалют = Обработки.ЗагрузкаКурсовВалют.КлассификаторВалют();
	
	СтрокаДанных = КлассификаторВалют.НайтиСтроки(Новый Структура("Code", "643"));
		
	Если СтрокаДанных.Количество() = 0 Тогда
		ВызватьИсключение "В классификаторе не найдена валюта с кодом 643";
	КонецЕсли;
	
	СтрокаДанных = СтрокаДанных[0];

	ВалютаВБазе = Справочники.Валюты.НайтиПоКоду(СтрокаДанных.Code);
	Если ЗначениеЗаполнено(ВалютаВБазе) Тогда
		Возврат ВалютаВБазе;
	КонецЕсли;

	НоваяСтрока = Справочники.Валюты.СоздатьЭлемент();
	НоваяСтрока.Код                       = СтрокаДанных.Code;
	НоваяСтрока.Наименование              = СтрокаДанных.CodeSymbol;
	НоваяСтрока.НаименованиеПолное        = СтрокаДанных.Name;
	Если СтрокаДанных.RBCLoading Тогда
		НоваяСтрока.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета;
	Иначе
		НоваяСтрока.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод;
	КонецЕсли;
	НоваяСтрока.ПараметрыПрописи = СтрокаДанных.NumerationItemOptions;
	НоваяСтрока.Записать();
	
	Возврат НоваяСтрока.Ссылка;
	
КонецФункции

#КонецОбласти