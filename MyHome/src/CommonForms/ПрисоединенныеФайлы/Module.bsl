#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.ВладелецФайлов) Тогда
		ВладелецФайлов = Параметры.ВладелецФайлов;
	Иначе
		Отказ = Истина;
		Сообщить("Форма предназначена только для открытия из формы владельца файлов");
	КонецЕсли
	;

	ИзменитьТекстЗапросаДинамическогоСписка();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьВидимостьЭлементовНаКлиенте();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);

	Состояние("Открытие файла...");

	ПрисоединенныеФайлыКлиент.ОткрытьПрисоединенныйФайл(ДанныеСтроки.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	УдалитьФайлНаКлиенте();

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СделатьФото(Команда)

	ДанныеФото = ПолучитьДанныеФотоснимка();

	Если ДанныеФото = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяФайла = Строка(Новый УникальныйИдентификатор);
	ИмяФайла = ИмяФайла + "." + ДанныеФото.РасширениеФайла;

	ДанныеФайла = ДанныеФото.ПолучитьДвоичныеДанные();
	АдресВХранилище = ПоместитьВоВременноеХранилище(ДанныеФайла);

	ПараметрыФайла = Новый Структура;
	ПараметрыФайла.Вставить("Хранение", АдресВХранилище);
	ПараметрыФайла.Вставить("Имя", ИмяФайла);

	ВыбратьФайлПослеПомещенияФайла(ПараметрыФайла, Неопределено);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьДанныеФотоснимка()

	Данные = Неопределено;

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда

	Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда

		ТипКамеры = ТипКамерыУстройства.Задняя;
		ТипПодсветки = ТипПодсветкиКамеры.Авто;

		Данные = СредстваМультимедиа.СделатьФотоснимок(ТипКамеры, , 100, Ложь, ТипПодсветки, Неопределено);

	Иначе
		ТекстСообщения = Нстр("ru = 'Устройство не поддерживает фотоснимки'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

#КонецЕсли

	Возврат Данные;

КонецФункции

&НаКлиенте
Процедура ДобавитьФайл(Команда)

	Оповещение = Новый ОписаниеОповещения("ВыбратьФайлПослеПомещенияФайла", ЭтотОбъект);
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки, , АдресВременногоХранилища);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайл(Команда)

	УдалитьФайлНаКлиенте();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементовНаКлиенте()

#Если МобильныйКлиент Или МобильноеПриложениеКлиент Тогда
	Элементы.СписокСделатьФото.Видимость = Истина;
	Элементы.ФормаДобавитьФайл.Заголовок = "С устройства";
	Элементы.ФормаДобавитьФайл.Отображение = ОтображениеКнопки.Текст;
	Элементы.СписокУдалитьФайл.Видимость = Ложь;
#Иначе
		Элементы.СписокСделатьФото.Видимость = Ложь;
		Элементы.ФормаДобавитьФайл.Заголовок = "Добавить";
		Элементы.ФормаДобавитьФайл.Отображение = ОтображениеКнопки.КартинкаИТекст;
		Элементы.СписокУдалитьФайл.Видимость = Истина;
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлНаКлиенте()

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДопПараметры = Новый Структура("ПрисоединенныйФайл", ТекущиеДанные.Ссылка);

	Оповещение = Новый ОписаниеОповещения("УдалитьФайлПослеВопроса", ЭтотОбъект, ДопПараметры);

	ПоказатьВопрос(Оповещение, "Удалить файл?", РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлПослеВопроса(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ПрисоединенныеФайлыКлиент.УдалитьПрисоединенныйФайл(ДопПараметры.ПрисоединенныйФайл);

	Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлПослеПомещенияФайла(Результат, ДопПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Результат.Вставить("ВладелецФайла", ВладелецФайлов);

	ПрисоединенныеФайлыВызовСервера.СоздатьПрисоединенныйФайл(Результат);

	Элементы.Список.Обновить();

КонецПроцедуры
&НаСервере
Процедура ИзменитьТекстЗапросаДинамическогоСписка()

	ИмяВладельца = ВладелецФайлов.Метаданные().Имя;
	ПолноеИмяПодчиненногоСправочника = "Справочник." + ИмяВладельца + "ПрисоединенныеФайлы";

	ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&ТаблицаФайлов", ПолноеИмяПодчиненногоСправочника);

	Список.ТекстЗапроса = ТекстЗапроса;
	Список.Параметры.УстановитьЗначениеПараметра("ВладелецФайла", ВладелецФайлов);

КонецПроцедуры
#КонецОбласти