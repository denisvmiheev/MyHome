
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьДанныеПароляИзБезопасногоХранилища();
	
	УстановитьВидимостьИндивидуальныхНастроек();
	
	ПрочитатьНастройкиРегламентногоЗадания();
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	ЗаписатьНастройкиРегламентногоЗадания(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьДанныеПароляВБезопасноеХранилище(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипВнешнейСистемыПриИзменении(Элемент)
	УстановитьВидимостьИндивидуальныхНастроек();	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентноеЗаданиеИспользуетсяПриИзменении(Элемент)
	УправлениеЭлементамиФормы(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйWuid(Команда)
	
	Объект.Тинькофф_wuid = мх_ИнтеграцияТинькоффКлиентСервер.НовыйУникальныйИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура Тинькофф_ОткрытьКонсоль(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВнешняяСистема", Объект.Ссылка);
	
	ОткрытьФорму("Обработка.мх_ИнтеграцияТинькофф.Форма.ФормаАвторизации",
				ПараметрыФормы,
				ЭтотОбъект,,,,,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьРасписание(Команда)
	
	РедактированиеРасписания = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	Результат = Ждать РедактированиеРасписания.ОткрытьАсинх();
	
	Если Результат <> Неопределено Тогда
		РасписаниеРегламентногоЗадания = Результат;
		Модифицированность = Истина;
		УстановитьНадписьРасписанияОбработки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьНастройкиРегламентногоЗадания()
	
	СтруктураРасписание = мх_РегламентныеЗадания.ПолучитьРасписаниеРегламентногоЗадания(Объект.ИдентификаторРегламентногоЗадания,
																						"мх_ЗагрузкаДанныхИзЛичныхКабинетовБанков");

	РасписаниеРегламентногоЗадания = СтруктураРасписание.Расписание;

	Если Объект.РегламентноеЗаданиеИспользуется <> СтруктураРасписание.Использование Тогда
		Объект.РегламентноеЗаданиеИспользуется	 = СтруктураРасписание.Использование;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиРегламентногоЗадания(ТекущийОбъект)
	
	ПредставлениеРасписания = "";
	Если Объект.РегламентноеЗаданиеИспользуется Тогда
		ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	КонецЕсли;
	ТекущийОбъект.ПредставлениеРасписания = ПредставлениеРасписания;

	мх_РегламентныеЗадания.УстановитьРасписаниеРегламентногоЗадания(ТекущийОбъект,
														 "мх_ЗагрузкаДанныхИзЛичныхКабинетовБанков",
														 РасписаниеРегламентногоЗадания,
														 Объект.РегламентноеЗаданиеИспользуется);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбработки()

	Если ДополнительныеОтчетыИОбработкиКлиентСервер.РасписаниеЗадано(РасписаниеРегламентногоЗадания) Тогда
		ТекстЗаголовка = Строка(РасписаниеРегламентногоЗадания);
	Иначе
		ТекстЗаголовка = НСтр("ru='Настроить расписание'");
	КонецЕсли;

	Элементы.ЗаполнитьРасписание.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИндивидуальныхНастроек()
	
	Для каждого ГруппаНастроек из Элементы.ИндивидуальныеНастройки.ПодчиненныеЭлементы Цикл
		
		ГруппаНастроек.Видимость = Ложь;
		
	КонецЦикла;
	
	ИмяНастройки = Строка(Объект.ТипВнешнейСистемы);
	
	Если ИмяНастройки <> "" Тогда
		
		ИмяГруппыНастроек = "Настройки" + ИмяНастройки;
		
		ГруппаНастроек = Элементы.Найти(ИмяГруппыНастроек);
		
		Если ГруппаНастроек <> Неопределено Тогда
			ГруппаНастроек.Видимость = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеПароляИзБезопасногоХранилища()
	
	Если НЕ Объект.Ссылка.Пустая() Тогда	
		УстановитьПривилегированныйРежим(Истина);
		ЗначениеПароля = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
		Пароль = ?(ЗначениеЗаполнено(ЗначениеПароля), ЭтотОбъект.УникальныйИдентификатор, "");		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеПароляВБезопасноеХранилище(ТекущийОбъект)
		
	Если ПарольИзменен Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, Пароль);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Форма.Элементы.ЗаполнитьРасписание.Доступность = Форма.Объект.РегламентноеЗаданиеИспользуется;
	
КонецПроцедуры

#КонецОбласти
