
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьДанныеПароляИзБезопасногоХранилища();
	
	УстановитьВидимостьИндивидуальныхНастроек();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьДанныеПароляВБезопасноеХранилище(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйWuid(Команда)
	
	Объект.Тинькофф_wuid = мх_ИнтеграцияТинькоффКлиентСервер.НовыйУникальныйИдентификатор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьИндивидуальныхНастроек()
	
	Для каждого ГруппаНастроек из Элементы.ИндивидуальныеНастройки.ПодчиненныеЭлементы Цикл
		
		ГруппаНастроек.Видимость = Ложь;
		
	КонецЦикла;
	
	ИмяПредопределенныхДанных = Объект.Ссылка.ИмяПредопределенныхДанных;
	
	Если ИмяПредопределенныхДанных <> "" Тогда
		
		ИмяГруппыНастроек = "Настройки" + ИмяПредопределенныхДанных;
		
		Элементы[ИмяГруппыНастроек].Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеПароляИзБезопасногоХранилища()
	
	Если НЕ Объект.Ссылка.Пустая() Тогда	
		УстановитьПривилегированныйРежим(Истина);
		ЗначениеПароля = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
		Пароль = ?(ЗначениеЗаполнено(ЗначениеПароля), ЭтотОбъект.УникальныйИдентификатор, "");		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеПароляВБезопасноеХранилище(ТекущийОбъект)
		
	Если ПарольИзменен Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, Пароль);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
