#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Остаток кошелька.
// 
// Параметры:
//  Кошелек - СправочникСсылка.мх_КошелькиИСчета - Кошелек, по которому считаются остатки
//  Период - Дата - Дата остатков. Если не указано, то подставляется ТекущаяДата()
// 
// Возвращаемое значение:
//  Структура - Остаток кошелька
//	 * - Валюта - СправочникСсылка.Валюты - Валюта кошелька
//	 * - СуммаОстаток - Число - Остаток
Функция ОстатокКошелька(Кошелек, Период = Неопределено) Экспорт

	Если Период = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	Иначе
		ДатаСреза = Период;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Кошелек", Кошелек);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.Валюта КАК Валюта,
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.мх_ДвиженияДенежныхСредств.Остатки(&ДатаСреза, Кошелек = &Кошелек) КАК
	|		ДвиженияДенежныхСредствОстатки";

	Выборка = Запрос.Выполнить().Выбрать();

	Результат = НоваяСтруктураОстаткаКошелька();

	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Остатки кошельков.
// 
// Параметры:
//  Кошельки - Массив - Кошельки, по которым считается остаток
//  Период - Дата - Дата остатков. Если не указано, то подставляется ТекущаяДата()
//  ВСоответствие - Булево - Если ИСТИНА, то результат преобразуется в соответствие
// 
// Возвращаемое значение:
//  ТаблицаЗначений, Соответствие - Остатки кошельков
Функция ОстаткиКошельков(Кошельки, Период = Неопределено, ВСоответствие = Истина) Экспорт

	Если Период = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	Иначе
		ДатаСреза = Период;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Кошельки", Кошельки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.Кошелек КАК Кошелек,
	|	ДвиженияДенежныхСредствОстатки.Валюта КАК Валюта,
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток * ЕСТЬNULL(КурсыВалют.Курс, 1) КАК СуммаОстатокВРублях
	|ИЗ
	|	РегистрНакопления.мх_ДвиженияДенежныхСредств.Остатки(&ДатаСреза, Кошелек В (&Кошельки)) КАК ДвиженияДенежныхСредствОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаСреза, ) КАК КурсыВалют
	|		ПО ДвиженияДенежныхСредствОстатки.Валюта = КурсыВалют.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДвиженияДенежныхСредствОстатки.Кошелек.Наименование УБЫВ";

	ТаблицаДанных = Запрос.Выполнить().Выгрузить();

	Результат = ТаблицаДанных;

	Если ВСоответствие Тогда

		Результат = Новый Соответствие;

		Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
			ДанныеБаланса = НоваяСтруктураОстаткаКошелька();
			ЗаполнитьЗначенияСвойств(ДанныеБаланса, СтрокаТаблицы);
			Результат.Вставить(СтрокаТаблицы.Кошелек, ДанныеБаланса);
		КонецЦикла;

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяСтруктураОстаткаКошелька()
	Возврат Новый Структура("Валюта, СуммаОстаток, СуммаОстатокВРублях", Неопределено, 0, 0);
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

Процедура ЗаполнитьВалютуКошельков() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	мх_КошелькиИСчета.Ссылка
	|ИЗ
	|	Справочник.мх_КошелькиИСчета КАК мх_КошелькиИСчета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВалютаПоУмолчанию = мх_КошелькиИСчета.ВалютаПоУмолчанию();
	
	Пока Выборка.Следующий() Цикл
		
		КошелекОбъект = Выборка.Ссылка.ПолучитьОбъект();
		КошелекОбъект.Валюта = ВалютаПоУмолчанию;
		КошелекОбъект.ОбменДанными.Загрузка = Истина;
		КошелекОбъект.Записать();
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли