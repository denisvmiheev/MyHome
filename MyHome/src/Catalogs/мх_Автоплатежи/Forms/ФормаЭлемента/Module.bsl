#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтруктураРасписание = АвтоплатежиСервер.ПолучитьРасписаниеАвтоплатежа(Объект.ИдентификаторРегламентногоЗадания);

	РасписаниеПлатежа	 = СтруктураРасписание.Расписание;

	Если Объект.Используется <> СтруктураРасписание.Использование Тогда
		Объект.Используется	 = СтруктураРасписание.Использование;
	КонецЕсли;

	мх_СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);

	УправлениеЭлементами();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьНадписьРасписанияОбработки();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ПредставлениеРасписания = "";
	Если Объект.Используется Тогда
		ПредставлениеРасписания = Строка(РасписаниеПлатежа);
	КонецЕсли;
	ТекущийОбъект.ПредставлениеРасписания = ПредставлениеРасписания;

	АвтоплатежиСервер.УстановитьРасписаниеАвтоплатежа(ТекущийОбъект, РасписаниеПлатежа, Объект.Используется);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользуетсяПриИзменении(Элемент)
	ИзменитьДоступностьКнопкиЗаполненияРасписания();
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДоходовРасходовПриИзменении(Элемент)

	ОчиститьЗначениеНеиспользуемойАналитики();
	УстановитьОграничениеТипаДляАналитики();

КонецПроцедуры

&НаКлиенте
Процедура ВидПлатежаПриИзменении(Элемент)

	УстановитьОграничениеТипаДляРеквизитаСтатьи();
	УстановитьВидимостьРеквизитаАналитики();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Асинх Процедура ЗаполнитьРасписание(Команда)

	РедактированиеРасписания = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеПлатежа);
	Результат = Ждать РедактированиеРасписания.ОткрытьАсинх();
	
	Если Результат <> Неопределено Тогда
		РасписаниеПлатежа = Результат;
		Модифицированность = Истина;
		УстановитьНадписьРасписанияОбработки();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОграничениеТипаДляРеквизитаСтатьи()

	МассивТипов = Новый Массив;

	Если Объект.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыДвиженийДенежныхСредств.Доход") Тогда
		МассивТипов.Добавить(Тип("ПланВидовХарактеристикСсылка.мх_СтатьиДоходов"));
	ИначеЕсли Объект.ВидПлатежа = ПредопределенноеЗначение("Перечисление.ВидыДвиженийДенежныхСредств.Расход") Тогда
		МассивТипов.Добавить(Тип("ПланВидовХарактеристикСсылка.мх_СтатьиРасходов"));
	Иначе
	КонецЕсли;

	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);

	Элементы.СтатьяДоходовРасходов.ОграничениеТипа = ОписаниеТипов;

	Объект.СтатьяДоходовРасходов = ОписаниеТипов.ПривестиЗначение();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбработки()

	Если ДополнительныеОтчетыИОбработкиКлиентСервер.РасписаниеЗадано(РасписаниеПлатежа) Тогда
		ТекстЗаголовка = Строка(РасписаниеПлатежа);
	Иначе
		ТекстЗаголовка = НСтр("ru='Настроить расписание'");
	КонецЕсли;

	Элементы.ЗаполнитьРасписание.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементами()

	ИзменитьДоступностьКнопкиЗаполненияРасписания();
	УстановитьОграничениеТипаДляРеквизитаСтатьи();
	УстановитьВидимостьРеквизитаАналитики();
	УстановитьОграничениеТипаДляАналитики();
	ОчиститьЗначениеНеиспользуемойАналитики();

КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьКнопкиЗаполненияРасписания()

	Элементы.ЗаполнитьРасписание.Доступность = Объект.Используется;

КонецПроцедуры

&НаСервере
Процедура ОчиститьЗначениеНеиспользуемойАналитики()

	Если ТипЗнч(Объект.СтатьяДоходовРасходов) = Тип("ПланВидовХарактеристикСсылка.мх_СтатьиДоходов") Тогда
		Объект.АналитикаРасходов = Неопределено;
	ИначеЕсли ТипЗнч(Объект.СтатьяДоходовРасходов) = Тип("ПланВидовХарактеристикСсылка.мх_СтатьиРасходов") Тогда
		Объект.АналитикаДоходов = Неопределено;
	Иначе
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОграничениеТипаДляАналитики()

	ЭлементФормыАналитика = Неопределено;

	Если Элементы.АналитикаДоходов.Видимость Тогда
		ЭлементФормыАналитика = Элементы.АналитикаДоходов;
	ИначеЕсли Элементы.АналитикаРасходов.Видимость Тогда
		ЭлементФормыАналитика = Элементы.АналитикаРасходов;
	Иначе
		Возврат;
	КонецЕсли;

	Если Объект.СтатьяДоходовРасходов <> Неопределено Тогда
		мх_СобытияФорм.УстановитьОграничениеТипаДляАналитикиДоходовРасходов(ЭлементФормыАналитика,
																			 Объект.СтатьяДоходовРасходов,
																			 Объект.АналитикаРасходов);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРеквизитаАналитики()

	Если ТипЗнч(Объект.СтатьяДоходовРасходов) = Тип("ПланВидовХарактеристикСсылка.мх_СтатьиДоходов") Тогда
		Элементы.АналитикаРасходов.Видимость = Ложь;
		Элементы.АналитикаДоходов.Видимость = Истина;
	ИначеЕсли ТипЗнч(Объект.СтатьяДоходовРасходов) = Тип("ПланВидовХарактеристикСсылка.мх_СтатьиРасходов") Тогда
		Элементы.АналитикаРасходов.Видимость = Истина;
		Элементы.АналитикаДоходов.Видимость = Ложь;
	Иначе
		Элементы.АналитикаРасходов.Видимость = Ложь;
		Элементы.АналитикаДоходов.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти