
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураРасписание = АвтоплатежиСервер.ПолучитьРасписаниеАвтоплатежа(Объект.ИдентификаторРегламентногоЗадания);
	
	РасписаниеПлатежа	 = СтруктураРасписание.Расписание;
	
	Если Объект.Используется <> СтруктураРасписание.Использование Тогда
		Объект.Используется	 = СтруктураРасписание.Использование;
	КонецЕсли;
	
	УправлениеЭлементами(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьНадписьРасписанияОбработки();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПредставлениеРасписания = "";
	Если Объект.Используется Тогда
		ПредставлениеРасписания = Строка(РасписаниеПлатежа);
	Конецесли;
	ТекущийОбъект.ПредставлениеРасписания = ПредставлениеРасписания;
	
	АвтоплатежиСервер.УстановитьРасписаниеАвтоплатежа(ТекущийОбъект, РасписаниеПлатежа, Объект.Используется);	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользуетсяПриИзменении(Элемент)
	УправлениеЭлементами(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаполнитьРасписание(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ПослеОкончанияРедактированияРасписания", ЭтотОбъект);
	
	РедактированиеРасписания = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеПлатежа);
	РедактированиеРасписания.Показать(Обработчик);

КонецПроцедуры

&НаКлиенте
Процедура ПослеОкончанияРедактированияРасписания(Расписание, ПараметрыОповещения) Экспорт
	
	Если Расписание <> Неопределено Тогда
		РасписаниеПлатежа = Расписание;
		Модифицированность = Истина;
		УстановитьНадписьРасписанияОбработки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбработки()
	
	Если ДополнительныеОтчетыИОбработкиКлиентСервер.РасписаниеЗадано(РасписаниеПлатежа) Тогда
		ТекстЗаголовка = Строка(РасписаниеПлатежа);
	Иначе
		ТекстЗаголовка = НСтр("ru='Настроить расписание'");
	КонецЕсли;
	
	Элементы.ЗаполнитьРасписание.Заголовок = ТекстЗаголовка;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементами(Форма)
	
	Элементы = Форма.Элементы;	
	Элементы.ЗаполнитьРасписание.Доступность = Форма.Объект.Используется;
	
КонецПроцедуры


#КонецОбласти