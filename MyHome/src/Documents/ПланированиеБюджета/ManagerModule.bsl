#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	ТаблицаПланированиеБюджета = ИнициализироватьТаблицуДвиженийПланированиеБюджета(ДокументОбъект);

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаПланированиеБюджета", ТаблицаПланированиеБюджета);

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияПланированиеБюджета(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПланированиеБюджета;

	Если Отказ Или ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДвиженияПланированиеБюджета = Документ.Движения.ПланированиеБюджета;
	ДвиженияПланированиеБюджета.Записывать = Истина;
	ДвиженияПланированиеБюджета.Загрузить(ТаблицаДвижений);

КонецПроцедуры

// Инициализирует таблицу движений по регистру "ПланированиеБюджета"
// 
// Параметры:
// 	ДокументОбъект - ДокументОбъект.ПланированиеБюджета - Регистратор
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание:
// * Регистратор 
// * Период 
// * ВидДвижения 
// * СтатьяДоходовРасходов 
// * Сумма 
// * СуммаБюджетНаДень 
Функция ИнициализироватьТаблицуДвиженийПланированиеБюджета(ДокументОбъект)

	ТаблицаДвижений = НоваяТаблицаДвиженийПланированиеБюджета();

	ДатаСч = ДокументОбъект.НачалоПериода;

	Пока ДатаСч <= ДокументОбъект.КонецПериода Цикл

		НоваяСтрока = ТаблицаДвижений.Добавить();
		НоваяСтрока.Период = ДатаСч;
		НоваяСтрока.Регистратор = ДокументОбъект.Ссылка;

		ДатаСч = ДатаСч + 86400;

	КонецЦикла;

	ТаблицаДвижений.ЗаполнитьЗначения(ДокументОбъект.СуммаБюджетНаДень, "СуммаБюджетНаДень");
	
	// Дополним движениями по доходам/расходам. Кидаем всё на первый день периода
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланированиеБюджетаДоходы.Ссылка КАК Регистратор,
	|	ПланированиеБюджетаДоходы.Ссылка.НачалоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПланированиеБюджетаДоходы.СтатьяДоходов КАК СтатьяДоходовРасходов,
	|	ПланированиеБюджетаДоходы.Сумма
	|ИЗ
	|	Документ.ПланированиеБюджета.Доходы КАК ПланированиеБюджетаДоходы
	|ГДЕ
	|	ПланированиеБюджетаДоходы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПланированиеБюджетаРасходы.Ссылка,
	|	ПланированиеБюджетаРасходы.Ссылка.НачалоПериода,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ПланированиеБюджетаРасходы.СтатьяРасходов,
	|	ПланированиеБюджетаРасходы.Сумма
	|ИЗ
	|	Документ.ПланированиеБюджета.Расходы КАК ПланированиеБюджетаРасходы
	|ГДЕ
	|	ПланированиеБюджетаРасходы.Ссылка = &Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();

	ЭтоПерваяСтрока = Истина;

	Пока Выборка.Следующий() Цикл

		Если ЭтоПерваяСтрока Тогда
			СтрокаТаблицы = ТаблицаДвижений.Найти(ДокументОбъект.НачалоПериода, "Период");

			Если СтрокаТаблицы = Неопределено Тогда
				НоваяСтрока = ТаблицаДвижений.Добавить();
			Иначе
				НоваяСтрока = СтрокаТаблицы;
			КонецЕсли;
		Иначе
			НоваяСтрока = ТаблицаДвижений.Добавить();
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

		ЭтоПерваяСтрока = Ложь;

	КонецЦикла;

	Возврат ТаблицаДвижений;

КонецФункции

// Подготавливает таблицу для формирования движений по регистру "ПланированиеБюджета"
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание:
// * Регистратор 
// * Период 
// * ВидДвижения 
// * СтатьяДоходовРасходов 
// * Сумма 
// * СуммаБюджетНаДень 
Функция НоваяТаблицаДвиженийПланированиеБюджета()

	ТаблицаДвижений = Новый ТаблицаЗначений;
	ТаблицаДвижений.Колонки.Добавить("Регистратор");
	ТаблицаДвижений.Колонки.Добавить("Период");
	ТаблицаДвижений.Колонки.Добавить("ВидДвижения");
	ТаблицаДвижений.Колонки.Добавить("СтатьяДоходовРасходов");
	ТаблицаДвижений.Колонки.Добавить("Сумма");
	ТаблицаДвижений.Колонки.Добавить("СуммаБюджетНаДень");

	Возврат ТаблицаДвижений;

КонецФункции

#КонецОбласти