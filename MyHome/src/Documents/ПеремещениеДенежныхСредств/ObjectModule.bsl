#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиОбъекта

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;

	ОбработчикиСобытийДокументов.ЗаполнитьРеквизитыДокументаПоШаблону(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Документы.ПеремещениеДенежныхСредств.ИнициализироватьТаблицыДляДвижений(ЭтотОбъект);

	Документы.ПеремещениеДенежныхСредств.ОтразитьДвиженияДенежныхСредств(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти()
	
	СтруктураЗаполнения = Новый Структура("КошелекОткуда, КошелекКуда, Сумма");
	
	ТаблицаПеремещений = ЭтотОбъект.Перемещения.Выгрузить();
	ТаблицаПеремещений.Свернуть("КошелекОткуда, КошелекКуда", "Сумма");
	
	Если ТаблицаПеремещений.Количество() = 1 Тогда
		
		ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ТаблицаПеремещений[0]);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли