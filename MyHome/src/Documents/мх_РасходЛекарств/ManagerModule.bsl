#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ДвиженияДокумента

Процедура ЗаблокироватьДанныеПередРегистрациейДвижений(Документ) Экспорт
	
	СписокНоменклатуры = Документ.Лекарства.Выгрузить(,"Номенклатура");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.мх_Лекарства");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	ТаблицаЛекарства = ПодготовитьТаблицуДвиженийЛекарства(ДокументОбъект);

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаЛекарства"	, ТаблицаЛекарства);

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Функция ПодготовитьТаблицуДвиженийЛекарства(ДокументОбъект)
	
	ДокументОбъект.Движения.мх_Лекарства.Записывать = Истина;
	ДокументОбъект.Движения.мх_Лекарства.Записать();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	мх_РасходЛекарствЛекарства.Номенклатура,
	|	мх_РасходЛекарствЛекарства.Единица,
	|	СУММА(мх_РасходЛекарствЛекарства.Количество) КАК Количество
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.мх_РасходЛекарств.Лекарства КАК мх_РасходЛекарствЛекарства
	|ГДЕ
	|	мх_РасходЛекарствЛекарства.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	мх_РасходЛекарствЛекарства.Номенклатура,
	|	мх_РасходЛекарствЛекарства.Единица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	мх_ЛекарстваОстатки.Номенклатура,
	|	мх_ЛекарстваОстатки.СрокГодности КАК СрокГодности,
	|	мх_ЛекарстваОстатки.Единица,
	|	мх_ЛекарстваОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.мх_Лекарства.Остатки(, (Номенклатура, Единица) В
	|		(ВЫБРАТЬ
	|			ДанныеДокумента.Номенклатура,
	|			ДанныеДокумента.Единица
	|		ИЗ
	|			ДанныеДокумента)) КАК мх_ЛекарстваОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокГодности";
	
	Результат = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ДанныеДокумента = Результат[0].Выгрузить();
	Остатки = Результат[1].Выгрузить();
	
	НаборЗаписей = РегистрыНакопления.мх_Лекарства.СоздатьНаборЗаписей();
	ТаблицаДвижений = НаборЗаписей.Выгрузить();

	Для каждого СтрокаДокумента из ДанныеДокумента Цикл
		
		СтруктураПоиска = Новый Структура("Номенклатура, Единица");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);
		
		СтрокиОстатков = Остатки.НайтиСтроки(СтруктураПоиска);		
		
		Для каждого СтрокаОстатков из СтрокиОстатков Цикл
			
			Если СтрокаДокумента.Количество = 0 Тогда
				Прервать;
			КонецЕсли;	
					
			Если СтрокаОстатков.КоличествоОстаток <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаОстатков.КоличествоОстаток >= СтрокаДокумента.Количество Тогда
				КЗаписи = СтрокаДокумента.Количество;
				СтрокаДокумента.Количество = 0;
				СтрокаОстатков.КоличествоОстаток = СтрокаОстатков.КоличествоОстаток - КЗаписи;
			Иначе
				КЗаписи = СтрокаОстатков.КоличествоОстаток;
				СтрокаДокумента.Количество = СтрокаДокумента.Количество - КЗаписи;
				СтрокаОстатков.КоличествоОстаток = 0;
			КонецЕсли;
			
			НоваяЗапись = ТаблицаДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаОстатков);
			НоваяЗапись.Количество = КЗаписи;
			
		КонецЦикла;
		
		Если СтрокаДокумента.Количество > 0 Тогда
			НоваяЗапись = ТаблицаДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДвижений.ЗаполнитьЗначения(ДокументОбъект.Ссылка, "Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(ДокументОбъект.Дата, "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	ТаблицаДвижений.ЗаполнитьЗначения(Истина, "Активность");
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Процедура ОтразитьДвиженияЛекарства(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЛекарства;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияЛекарства = Документ.Движения.мх_Лекарства;
	ДвиженияЛекарства.Записывать = Истина;
	ДвиженияЛекарства.Загрузить(ТаблицаДвижений);
	ДвиженияЛекарства.Записать();

КонецПроцедуры

Процедура КонтрольРезультатовПроведения(Документ, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Документ.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Лекарства.Номенклатура,
	|	Лекарства.Единица
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.мх_РасходЛекарств.Лекарства КАК Лекарства
	|ГДЕ
	|	Лекарства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	мх_ЛекарстваОстатки.Номенклатура,
	|	мх_ЛекарстваОстатки.СрокГодности,
	|	мх_ЛекарстваОстатки.Единица,
	|	мх_ЛекарстваОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.мх_Лекарства.Остатки(, (Номенклатура, Единица) В
	|		(ВЫБРАТЬ
	|			ДанныеДокумента.Номенклатура,
	|			ДанныеДокумента.Единица
	|		ИЗ
	|			ДанныеДокумента)) КАК мх_ЛекарстваОстатки
	|ГДЕ
	|	мх_ЛекарстваОстатки.КоличествоОстаток < 0";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ШаблонОшибки = НСтр("ru = 'По %1 расходуется больше чем есть в наличии на %2 %3'");
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Выборка.Номенклатура, -Выборка.КоличествоОстаток, Выборка.Единица);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,,Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли