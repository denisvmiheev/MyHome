#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытийОбъекта

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;

	мх_ОбработчикиСобытий.ЗаполнитьРеквизитыДокументаПоШаблону(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

	ЗаполнитьРеквизитыСумм();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Документы.мх_ПланированиеБюджета.ИнициализироватьТаблицыДляДвижений(ЭтотОбъект);

	Документы.мх_ПланированиеБюджета.ОтразитьДвиженияПланированиеБюджета(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.мх_ПланированиеБюджета") Тогда
		ЗаполнитьПоДокументуПланирования(ДанныеЗаполнения);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыСумм()

	СуммаДоход = Доходы.Итог("Сумма");
	СуммаРасход = Расходы.Итог("Сумма");

КонецПроцедуры

Процедура ЗаполнитьПоДокументуПланирования(ДокументПланирования)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументПланирования);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПланированиеБюджетаДоходы.СтатьяДоходов,
	|	ПланированиеБюджетаДоходы.Сумма
	|ИЗ
	|	Документ.мх_ПланированиеБюджета.Доходы КАК ПланированиеБюджетаДоходы
	|ГДЕ
	|	ПланированиеБюджетаДоходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланированиеБюджетаРасходы.СтатьяРасходов,
	|	ПланированиеБюджетаРасходы.Сумма
	|ИЗ
	|	Документ.мх_ПланированиеБюджета.Расходы КАК ПланированиеБюджетаРасходы
	|ГДЕ
	|	ПланированиеБюджетаРасходы.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаДоходы = РезультатЗапроса[0].Выгрузить();
	ТаблицаРасходы = РезультатЗапроса[1].Выгрузить();
	
	ЭтотОбъект.Доходы.Загрузить(ТаблицаДоходы);
	ЭтотОбъект.Расходы.Загрузить(ТаблицаРасходы);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли