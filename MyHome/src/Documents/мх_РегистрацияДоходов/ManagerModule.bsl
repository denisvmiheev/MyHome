#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	мх_МобильныйИнтерфейс.ОбработкаПолученияФормы(ВидФормы,
												Параметры,
											 	ВыбраннаяФорма,
											 	ДополнительнаяИнформация,
											 	СтандартнаяОбработка,
											 	"Документ.мх_РегистрацияДоходов");	

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Создает документ "Регистрация дохода"
// 
// Параметры:
// 	ПараметрыРасхода - Структура
// - Обязательные параметры
// 	* Сумма - Число - Сумма дохода
// 	* Кошелек - СправочникСсылка.мх_КошелькиИСчета
//  * СтатьяРасходов - ПланВидовХарактеристикСсылка.мх_СтатьиДоходов
// - Необязательные параметры
// 	* ДатаОперации - Дата - Дата платежа. Если не указана, то используется текущая дата
// 	* АналитикаДоходов - Характеристика.мх_СтатьиДоходов - Аналитика дохода
// 	* Содержание - Строка - содержание операции
// Возвращаемое значение:
// 	Структура - Описание:
// * Успех - Булево - Результат создания документа
// * ОписаниеОшибки - Строка - Описание ошибки
Функция ДобавитьДоход(ПараметрыДохода) Экспорт

	Результат = Новый Структура("Успех, ОписаниеОшибки", Истина, "");

	ТекущаяДата = ТекущаяДата();

	Если Не ПараметрыДохода.Свойство("ДатаОперации") Тогда
		ПараметрыДохода.Вставить("ДатаОперации", ТекущаяДата);
	КонецЕсли;

	ДокументДохода = Документы.мх_РегистрацияДоходов.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(ДокументДохода, ПараметрыДохода);
	ДокументДохода.Дата = ТекущаяДата;
	НоваяСтрока = ДокументДохода.Доходы.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыДохода);

	Попытка
		ДокументДохода.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка КАК Регистратор,
	|	ТаблицаДоходы.ДатаОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДоходы.Кошелек КАК Кошелек,
	|	ТаблицаДоходы.Кошелек.Валюта КАК Валюта,
	|	СУММА(ТаблицаДоходы.Сумма) КАК Сумма
	|ИЗ
	|	Документ.мх_РегистрацияДоходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходы.Кошелек,
	|	ТаблицаДоходы.Ссылка,
	|	ТаблицаДоходы.ДатаОперации,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка КАК Регистратор,
	|	ТаблицаДоходы.ДатаОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДоходы.НомерСтроки,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходовРасходов,
	|	ТаблицаДоходы.Кошелек.Валюта КАК Валюта,
	|	ТаблицаДоходы.Сумма
	|ИЗ
	|	Документ.мх_РегистрацияДоходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка КАК Регистратор,
	|	ТаблицаДоходы.ДатаОперации КАК Период,
	|	ТаблицаДоходы.НомерСтроки,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов,
	|	ТаблицаДоходы.Кошелек.Валюта КАК Валюта,
	|	ТаблицаДоходы.Содержание КАК Содержание,
	|	ТаблицаДоходы.Сумма
	|ИЗ
	|	Документ.мх_РегистрацияДоходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка КАК Регистратор,
	|	ТаблицаДоходы.ДатаОперации КАК Период,
	|	МИНИМУМ(ТаблицаДоходы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходовРасходов,
	|	СУММА(ТаблицаДоходы.Сумма) КАК СуммаБюджетНаДень
	|ИЗ
	|	Документ.мх_РегистрацияДоходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|	И ТаблицаДоходы.ВнеплановыйДоход
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходы.Ссылка,
	|	ТаблицаДоходы.ДатаОперации,
	|	ТаблицаДоходы.СтатьяДоходов";

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаДвиженияДенежныхСредств"	, РезультатЗапроса[0].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаДоходыРасходы"				, РезультатЗапроса[1].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаДоходы"					, РезультатЗапроса[2].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаПланированиеБюджета"		, РезультатЗапроса[3].Выгрузить());

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияДенежныхСредств(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДвиженияДенежныхСредств;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДенежныхСредств = Документ.Движения.мх_ДвиженияДенежныхСредств;
	ДвиженияДенежныхСредств.Записывать = Истина;
	ДвиженияДенежныхСредств.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Процедура ОтразитьДвиженияДоходыРасходы(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыРасходы;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДоходыРасходы = Документ.Движения.мх_ДоходыРасходы;
	ДвиженияДоходыРасходы.Записывать = Истина;
	ДвиженияДоходыРасходы.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Процедура ОтразитьДвиженияДоходы(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходы;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДоходы = Документ.Движения.мх_Доходы;
	ДвиженияДоходы.Записывать = Истина;
	ДвиженияДоходы.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Процедура ОтразитьДвиженияПланированиеБюджета(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПланированиеБюджета;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДоходы = Документ.Движения.мх_ПланированиеБюджета;
	ДвиженияДоходы.Записывать = Истина;
	ДвиженияДоходы.Загрузить(ТаблицаДвижений);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли