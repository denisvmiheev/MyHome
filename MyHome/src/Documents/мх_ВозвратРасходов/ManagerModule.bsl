#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	мх_МобильныйИнтерфейс.ОбработкаПолученияФормы(ВидФормы,
												Параметры,
											 	ВыбраннаяФорма,
											 	ДополнительнаяИнформация,
											 	СтандартнаяОбработка,
											 	"Документ.мх_ВозвратРасходов");	

КонецПроцедуры

#КонецОбласти
	
#Область ПрограммныйИнтерфейс

// Таблица расходов по документам расхода.
// 
// Параметры:
//  ДокументыРасхода - ДокументСсылка.мх_РегистрацияДоходов, Массив - Документы расхода
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица расходов по документам расхода
Функция ТаблицаРасходовПоДокументамРасхода(ДокументыРасхода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыРасхода", ДокументыРасхода);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияРасходовРасходы.Кошелек,
	|	РегистрацияРасходовРасходы.Кошелек.Валюта КАК Валюта,
	|	РегистрацияРасходовРасходы.СтатьяРасходов,
	|	РегистрацияРасходовРасходы.АналитикаРасходов,
	|	РегистрацияРасходовРасходы.Сумма,
	|	РегистрацияРасходовРасходы.Содержание,
	|	РегистрацияРасходовРасходы.ДатаОперации,
	|	РегистрацияРасходовРасходы.Ссылка КАК ДокументРасхода
	|ИЗ
	|	Документ.мх_РегистрацияРасходов.Расходы КАК РегистрацияРасходовРасходы
	|ГДЕ
	|	РегистрацияРасходовРасходы.Ссылка В (&ДокументыРасхода)";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// Дерево расходов за период.
// 
// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
// 
// Возвращаемое значение:
//  ДеревоЗначений - Дерево расходов за период
Функция ДеревоРасходовЗаПериод(ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(1)) КАК ПредставлениеСтроки,
	|	ЛОЖЬ КАК Подобрать,
	|	Расходы.Период КАК ДатаОперации,
	|	Расходы.Регистратор КАК ДокументРасхода,
	|	Расходы.СтатьяРасходов,
	|	Расходы.АналитикаРасходов,
	|	Расходы.Сумма КАК Сумма,
	|	Расходы.Содержание
	|ИЗ
	|	РегистрНакопления.мх_Расходы КАК Расходы
	|ГДЕ
	|	Расходы.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ДокументРасхода";
	
	ДеревоРасходов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ДеревоРасходов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРасходы.Ссылка КАК Регистратор,
	|	ТаблицаРасходы.ДатаОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаРасходы.Кошелек КАК Кошелек,
	|	ТаблицаРасходы.Кошелек.Валюта КАК Валюта,
	|	СУММА(ТаблицаРасходы.Сумма) КАК Сумма
	|ИЗ
	|	Документ.мх_ВозвратРасходов.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасходы.Кошелек,
	|	ТаблицаРасходы.Ссылка,
	|	ТаблицаРасходы.ДатаОперации,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасходы.Ссылка КАК Регистратор,
	|	ТаблицаРасходы.ДатаОперации КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаРасходы.НомерСтроки,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяДоходовРасходов,
	|	ТаблицаРасходы.Кошелек.Валюта КАК Валюта,
	|	-ТаблицаРасходы.Сумма КАК Сумма
	|ИЗ
	|	Документ.мх_ВозвратРасходов.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасходы.Ссылка КАК Регистратор,
	|	ТаблицаРасходы.ДатаОперации КАК Период,
	|	ТаблицаРасходы.НомерСтроки,
	|	ТаблицаРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов,
	|	ТаблицаРасходы.Содержание КАК Содержание,
	|	ТаблицаРасходы.Кошелек.Валюта КАК Валюта,
	|	-ТаблицаРасходы.Сумма КАК Сумма,
	|	ТаблицаРасходы.ВнеБюджета
	|ИЗ
	|	Документ.мх_ВозвратРасходов.Расходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка";

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаДвиженияДенежныхСредств", РезультатЗапроса[0].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаДоходыРасходы", РезультатЗапроса[1].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаРасходы", РезультатЗапроса[2].Выгрузить());

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияДенежныхСредств(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДвиженияДенежныхСредств;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДенежныхСредств = Документ.Движения.мх_ДвиженияДенежныхСредств;
	ДвиженияДенежныхСредств.Записывать = Истина;
	ДвиженияДенежныхСредств.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Процедура ОтразитьДвиженияДоходыРасходы(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыРасходы;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДоходыРасходы = Документ.Движения.мх_ДоходыРасходы;
	ДвиженияДоходыРасходы.Записывать = Истина;
	ДвиженияДоходыРасходы.Загрузить(ТаблицаДвижений);

КонецПроцедуры

Процедура ОтразитьДвиженияРасходы(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасходы;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ДвиженияРасходы = Документ.Движения.мх_Расходы;
	ДвиженияРасходы.Записывать = Истина;
	ДвиженияРасходы.Загрузить(ТаблицаДвижений);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли