#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиОбъекта

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;

	мх_ОбработчикиСобытий.ЗаполнитьРеквизитыДокументаПоШаблону(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	мх_ОбработчикиСобытий.ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти(ЭтотОбъект,
		"Расходы",
		"Кошелек, СтатьяРасходов, АналитикаРасходов, ДатаОперации, ВнеБюджета",
		"Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.мх_ВозвратРасходов.ИнициализироватьТаблицыДляДвижений(ЭтотОбъект);

	Документы.мх_ВозвратРасходов.ОтразитьДвиженияДенежныхСредств(ЭтотОбъект, Отказ);
	Документы.мх_ВозвратРасходов.ОтразитьДвиженияДоходыРасходы(ЭтотОбъект, Отказ);
	Документы.мх_ВозвратРасходов.ОтразитьДвиженияРасходы(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.мх_РегистрацияРасходов") Тогда
		ЗаполнитьПоДокументуРасхода(ДанныеЗаполнения);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыШапкиПоДаннымТабличнойЧасти()
	
	ЭтотОбъект.Сумма = Расходы.Итог("Сумма");
	
	ДокументыРасхода = ЭтотОбъект.Расходы.Выгрузить();
	ДокументыРасхода.Свернуть("ДокументРасхода");
	
	Если ДокументыРасхода.Количество() = 1 Тогда
		ЭтотОбъект.ДокументРасхода = ДокументыРасхода[0].ДокументРасхода;
	Иначе
		ЭтотОбъект.ДокументРасхода = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуРасхода(ДокументРасхода)
	
	ТаблицаРасходов = Документы.мх_ВозвратРасходов.ТаблицаРасходовПоДокументамРасхода(ДокументРасхода);
	
	ЭтотОбъект.Расходы.Загрузить(ТаблицаРасходов);
	
	ДатаОперации = ОбщегоНазначения.ТекущаяДатаПользователя();
	Для каждого СтрокаТаблицы Из ЭтотОбъект.Расходы Цикл
		СтрокаТаблицы.ДатаОперации = ДатаОперации; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



