#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	мх_МобильныйИнтерфейс.ОбработкаПолученияФормы(ВидФормы,
												Параметры,
											 	ВыбраннаяФорма,
											 	ДополнительнаяИнформация,
											 	СтандартнаяОбработка,
											 	"Документ.мх_Гарантия");	

КонецПроцедуры

#КонецОбласти	
	
#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГарантияТовары.Ссылка,
	|	ГарантияТовары.Номенклатура,
	|	ГарантияТовары.ДатаПокупки,
	|	ГарантияТовары.ГарантийныйСрок,
	|	ГарантияТовары.ГарантияМес
	|ИЗ
	|	Документ.мх_Гарантия.Товары КАК ГарантияТовары
	|ГДЕ
	|	ГарантияТовары.Ссылка = &Ссылка";

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ДвиженияГарантия", РезультатЗапроса[0].Выгрузить());

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияГарантия(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ДвиженияГарантия;

	Если Отказ Или ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДвиженияГарантия = Документ.Движения.мх_Гарантия;
	ДвиженияГарантия.Записывать = Истина;
	ДвиженияГарантия.Загрузить(ТаблицаДвижений);

КонецПроцедуры

#КонецОбласти

#КонецЕсли