#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт

	Граница = Новый Граница(ДокументОбъект.МоментВремени(), ВидГраницы.Исключая);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Граница", Граница);
	Запрос.УстановитьПараметр("Кошелек", ДокументОбъект.ОбъектУчета);
	Запрос.УстановитьПараметр("Дата", ДокументОбъект.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.Кошелек,
	|	-ДвиженияДенежныхСредствОстатки.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ ПредварительныеДанные
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств.Остатки(&Граница, Кошелек = &Кошелек) КАК ДвиженияДенежныхСредствОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВводОстатков.ОбъектУчета,
	|	ВводОстатков.Сумма
	|ИЗ
	|	Документ.ВводОстатков КАК ВводОстатков
	|ГДЕ
	|	ВводОстатков.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПредварительныеДанные.Кошелек,
	|	СУММА(ПредварительныеДанные.Сумма) КАК Сумма
	|ИЗ
	|	ПредварительныеДанные КАК ПредварительныеДанные
	|СГРУППИРОВАТЬ ПО
	|	ПредварительныеДанные.Кошелек,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)";

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаДвиженияДенежныхСредств", РезультатЗапроса[1].Выгрузить());

	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);

КонецПроцедуры

Процедура ОтразитьДвиженияДенежныхСредств(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДвиженияДенежныхСредств;

	Если Отказ Или ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДвиженияДенежныхСредств = Документ.Движения.ДвиженияДенежныхСредств;
	ДвиженияДенежныхСредств.Записывать = Истина;
	ДвиженияДенежныхСредств.Загрузить(ТаблицаДвижений);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
