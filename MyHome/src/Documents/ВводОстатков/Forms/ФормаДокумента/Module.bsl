#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийОстаток = ТекущийОстатокКошелькаНаСервере(Объект.Дата, Объект.ОбъектУчета, Объект.Ссылка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ТекущийОстатокКошелька(Объект.Дата, Объект.ОбъектУчета, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектУчетаПриИзменении(Элемент)
	ТекущийОстатокКошелька(Объект.Дата, Объект.ОбъектУчета, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТекущийОстатокКошелька(ДатаОстатка, Кошелек, Ссылка)

	Если Не ЗначениеЗаполнено(ДатаОстатка) Или Не ЗначениеЗаполнено(Кошелек) Тогда

		ТекущийОстаток = 0;

	Иначе

		ТекущийОстаток = ТекущийОстатокКошелькаНаСервере(ДатаОстатка, Кошелек, Объект.Ссылка);

	КонецЕсли;

КонецПроцедуры
&НаСервереБезКонтекста
Функция ТекущийОстатокКошелькаНаСервере(ДатаОстатка, Кошелек, Ссылка)

	Если Не ЗначениеЗаполнено(ДатаОстатка) Или Не ЗначениеЗаполнено(Кошелек) Тогда

		Возврат 0;

	КонецЕсли;

	Если Ссылка.Пустая() Тогда
		Граница = Новый Граница(ДатаОстатка, ВидГраницы.Включая);
	Иначе
		МоментВремени = Новый МоментВремени(ДатаОстатка, Ссылка);
		Граница = Новый Граница(МоментВремени, ВидГраницы.Исключая);
	КонецЕсли
	;

	СуммаОстаток = 0;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кошелек", Кошелек);
	Запрос.УстановитьПараметр("Граница", Граница);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств.Остатки(&Граница, &Кошелек = Кошелек) КАК ДвиженияДенежныхСредствОстатки";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СуммаОстаток = Выборка.СуммаОстаток;
	КонецЦикла;

	Возврат СуммаОстаток;

КонецФункции

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти