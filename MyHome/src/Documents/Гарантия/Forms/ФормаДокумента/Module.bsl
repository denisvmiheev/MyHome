#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.Товары.Количество() = 0 Тогда
		Объект.Товары.Добавить();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НесколькоОпераций = Объект.Товары.Количество() > 1;
	УстановитьВидимостьСтраницыОпераций();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТоварыДатаПокупкиПриИзменении(Элемент)

	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ДатаПокупки");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыГарантийныйСрокПриИзменении(Элемент)

	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;

	Если ДанныеСтроки.ДатаПокупки > ДанныеСтроки.ГарантийныйСрок Тогда
		ДанныеСтроки.ГарантийныйСрок = Дата(1, 1, 1);
		ОбщегоНазначенияКлиент.СообщитьПользователю("Гарантийный срок не может быть меньше даты покупки");
		Возврат;
	КонецЕсли;

	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ГарантийныйСрок");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыГарантияМесПриИзменении(Элемент)

	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ГарантияМес");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыДатаПокупкиОднаОперацияПриИзменении(Элемент)

	ИдентификаторСтроки = Объект.Товары[0].ПолучитьИдентификатор();
	ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ДатаПокупки");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыГарантияМесОднаОперацияПриИзменении(Элемент)

	ИдентификаторСтроки = Объект.Товары[0].ПолучитьИдентификатор();
	ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ГарантияМес");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыГарантийныйСрокОднаОперацияПриИзменении(Элемент)

	ИдентификаторСтроки = Объект.Товары[0].ПолучитьИдентификатор();
	ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);

	Если ДанныеСтроки.ДатаПокупки > ДанныеСтроки.ГарантийныйСрок Тогда
		ДанныеСтроки.ГарантийныйСрок = Дата(1, 1, 1);
		ОбщегоНазначенияКлиент.СообщитьПользователю("Гарантийный срок не может быть меньше даты покупки");
		Возврат;
	КонецЕсли;

	ПересчитатьСрокиПоСтроке(ДанныеСтроки, "ГарантийныйСрок");

КонецПроцедуры

&НаКлиенте
Процедура НесколькоОперацийПриИзменении(Элемент)
	УстановитьВидимостьСтраницыОпераций();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьСтраницыОпераций()

	Если НесколькоОпераций Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНесколькоОпераций;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОднаОперация;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСрокиПоСтроке(ДанныеСтроки, ЧтоИзменилось)

	ПустаяДата = Дата(1, 1, 1);

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ДатаПокупки", ПустаяДата);
	СтруктураРезультата.Вставить("ГарантийныйСрок", ПустаяДата);
	СтруктураРезультата.Вставить("ГарантияМес", 0);

	ЗаполнитьЗначенияСвойств(СтруктураРезультата, ДанныеСтроки);

	Если ЧтоИзменилось = "ДатаПокупки" Тогда
		СтруктураРезультата.ГарантийныйСрок = ДобавитьМесяц(СтруктураРезультата.ДатаПокупки, СтруктураРезультата.ГарантияМес);
	ИначеЕсли ЧтоИзменилось = "ГарантийныйСрок" Тогда
		СтруктураРезультата.ГарантияМес = РазностьДатВМесяцах(СтруктураРезультата.ДатаПокупки, СтруктураРезультата.ГарантийныйСрок);
	ИначеЕсли ЧтоИзменилось = "ГарантияМес" Тогда
		СтруктураРезультата.ГарантийныйСрок = ДобавитьМесяц(СтруктураРезультата.ДатаПокупки, СтруктураРезультата.ГарантияМес);
	Иначе
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтруктураРезультата);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазностьДатВМесяцах(ДатаНачала, ДатаОкончания)

	Годы = Год(ДатаОкончания) - Год(ДатаНачала);
	Месяцы = Месяц(ДатаОкончания) - Месяц(ДатаНачала);
	Разность = Месяцы + Годы * 12;
	Возврат Разность;

КонецФункции

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти