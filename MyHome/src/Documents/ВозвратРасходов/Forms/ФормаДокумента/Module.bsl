#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.Расходы.Количество() = 0 Тогда
		Объект.Расходы.Добавить();
	КонецЕсли;

	СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);

	СобытияФормВызовСервера.УстановитьОграничениеТипаДляАналитикиДоходовРасходов(Элементы.РасходыАналитикаРасходовТекущиеДанные,
		Объект.Расходы[0].СтатьяРасходов);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НесколькоОпераций = Объект.Расходы.Количество() > 1;
	УстановитьВидимостьСтраницыОпераций();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НесколькоОперацийПриИзменении(Элемент)
	УстановитьВидимостьСтраницыОпераций();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСтатьяДоходов1ПриИзменении(Элемент)
	ДоходыСтатьяДоходов1ПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДоходыСтатьяДоходов1ПриИзмененииНаСервере()
	СобытияФормВызовСервера.УстановитьОграничениеТипаДляАналитикиДоходовРасходов(Элементы.РасходыАналитикаРасходовТекущиеДанные,
		Объект.Расходы[0].СтатьяРасходов);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоДокументуРасхода(Команда)

	Если Объект.Расходы.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена.
							|Продолжить?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоДокументуРасходаПослеВопроса", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоДокументуРасходаНаКлиенте();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодобратьРасходы(Команда)
	
	ЗаполнитьПодборомНаКлиенте();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПодборомНаКлиенте()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПериодРасходов", Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц));

	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПодборомЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.ВозвратРасходов.Форма.ПодборРасходов", ПараметрыФормы, ЭтотОбъект, КлючУникальности, , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуРасходаПослеВопроса(Результат, ДопПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДокументуРасходаНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуРасходаНаКлиенте()

	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоДокументуРасходаЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("МножественныйВыбор", Истина);
	ОткрытьФорму("Документ.РегистрацияРасходов.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, КлючУникальности, , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуРасходаЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДокументуРасходаНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДокументуРасходаНаСервере(Результат)

	ТаблицаРасходов = Документы.ВозвратРасходов.ТаблицаРасходовПоДокументамРасхода(Результат);
	
	Объект.Расходы.Загрузить(ТаблицаРасходов);	

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодборомЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаДанных из Результат Цикл
		
		НоваяСтрока = Объект.Расходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьСтраницыОпераций()

	Если НесколькоОпераций Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНесколькоОпераций;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОднаОперация;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти