
#Область ДвиженияДокумента

Процедура ИнициализироватьТаблицыДляДвижений(ДокументОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка.Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДатаДохода = &ПустаяДата
	|			ТОГДА ТаблицаДоходы.Ссылка.НачалоПериода
	|		ИНАЧЕ ДатаДохода
	|	КОНЕЦ КАК Период,
	|	ТаблицаДоходы.СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаДоходы.Сумма
	|ИЗ
	|	Документ.ПланированиеДоходовРасходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасходы.Ссылка.Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДатаРасхода = &ПустаяДата
	|			ТОГДА ТаблицаРасходы.Ссылка.НачалоПериода
	|		ИНАЧЕ ДатаРасхода
	|	КОНЕЦ КАК Период,
	|	ТаблицаРасходы.СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов,
	|	ТаблицаРасходы.Сумма
	|ИЗ
	|	Документ.ПланированиеДоходовРасходов.ОбязательныеРасходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДоходы.Ссылка.Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДатаДохода = &ПустаяДата
	|			ТОГДА ТаблицаДоходы.Ссылка.НачалоПериода
	|		ИНАЧЕ ДатаДохода
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДоходы.СтатьяДоходов КАК СтатьяДоходовРасходов,
	|	ТаблицаДоходы.Сумма
	|ИЗ
	|	Документ.ПланированиеДоходовРасходов.Доходы КАК ТаблицаДоходы
	|ГДЕ
	|	ТаблицаДоходы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасходы.Ссылка.Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДатаРасхода = &ПустаяДата
	|			ТОГДА ТаблицаРасходы.Ссылка.НачалоПериода
	|		ИНАЧЕ ДатаРасхода
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаРасходы.СтатьяРасходов,
	|	ТаблицаРасходы.Сумма
	|ИЗ
	|	Документ.ПланированиеДоходовРасходов.ОбязательныеРасходы КАК ТаблицаРасходы
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТаблицаДоходы"		, РезультатЗапроса[0].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаРасходы"		, РезультатЗапроса[1].Выгрузить());
	СтруктураРезультата.Вставить("ТаблицаДоходыРасходы"	, РезультатЗапроса[2].Выгрузить());
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", СтруктураРезультата);
	
КонецПроцедуры

Процедура ОтразитьДвиженияДоходыРасходы(Документ, Отказ) Экспорт
	
	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыРасходы;
	
	Если Отказ ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияДоходыРасходы = Документ.Движения.ДоходыРасходы;
	ДвиженияДоходыРасходы.Записывать = Истина;
	ДвиженияДоходыРасходы.Загрузить(ТаблицаДвижений);
	
КонецПроцедуры

Процедура ОтразитьДвиженияДоходы(Документ, Отказ) Экспорт

	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходы;
	
	Если Отказ ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияДоходы = Документ.Движения.Доходы;
	ДвиженияДоходы.Записывать = Истина;
	ДвиженияДоходы.Загрузить(ТаблицаДвижений);
		
КонецПроцедуры

Процедура ОтразитьДвиженияРасходы(Документ, Отказ) Экспорт
	
	ТаблицаДвижений = Документ.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасходы;
	
	Если Отказ ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияРасходы = Документ.Движения.Расходы;
	ДвиженияРасходы.Записывать = Истина;
	ДвиженияРасходы.Загрузить(ТаблицаДвижений);
		
КонецПроцедуры

#КонецОбласти