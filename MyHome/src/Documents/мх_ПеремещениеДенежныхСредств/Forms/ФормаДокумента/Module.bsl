
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.Перемещения.Количество() = 0 Тогда
		НоваяСтрока = Объект.Перемещения.Добавить();
		НоваяСтрока.ДатаОперации = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;                                            
	
	ЗаполнитьВалюты();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	НесколькоОпераций = Объект.Перемещения.Количество() > 1;
	УстановитьВидимостьСтраницыОпераций();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НесколькоОперацийПриИзменении(Элемент)
	УстановитьВидимостьСтраницыОпераций();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыПеремещения

&НаКлиенте
Процедура ПеремещенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	мх_СобытияФормКлиент.ДоходыРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещенияСуммаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Перемещения.ТекущиеДанные;	
	ЗаполнитьСуммуКуда(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПеремещенияСуммаКудаПриИзменении(Элемент)	
	ТекущиеДанные = Элементы.Перемещения.ТекущиеДанные;
	ЗаполнитьСуммуКуда(ТекущиеДанные);	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещенияСумма1ПриИзменении(Элемент)
	ТекущиеДанные = Объект.Перемещения[0];	
	ЗаполнитьСуммуКуда(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура СуммаКудаПриИзменении(Элемент)
	ТекущиеДанные = Объект.Перемещения[0];	
	ЗаполнитьСуммуКуда(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПеремещенияКошелекОткудаПриИзменении(Элемент)	
	ТекущиеДанные = Элементы.Перемещения.ТекущиеДанные;
	ТекущиеДанные.ВалютаКошелькаОткуда = ВалютаКошелька(ТекущиеДанные.КошелекОткуда);
	ЗаполнитьСуммуКуда(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПеремещенияКошелекКудаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Перемещения.ТекущиеДанные;
	ТекущиеДанные.ВалютаКошелькаКуда = ВалютаКошелька(ТекущиеДанные.КошелекКуда);
	ЗаполнитьСуммуКуда(ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьСтраницыОпераций()

	мх_СобытияФормКлиент.УстановитьВидимостьСтраницыОпераций(ЭтотОбъект, "Перемещения");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСуммуКуда(ДанныеСтроки)
	Если ДанныеСтроки.ВалютаКошелькаОткуда = ДанныеСтроки.ВалютаКошелькаКуда Тогда
		ДанныеСтроки.СуммаКуда = ДанныеСтроки.Сумма;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВалютаКошелька(Знач Кошелек)
	Возврат мх_КошелькиИСчета.ВалютаКошелька(Кошелек);
КонецФункции

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ЗаполнитьВалюты()
	
	Таблица = Объект.Перемещения.Выгрузить(,"КошелекОткуда, КошелекКуда");
	
	Кошельки = Таблица.ВыгрузитьКолонку("КошелекОткуда");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Кошельки, Таблица.ВыгрузитьКолонку("КошелекКуда"));
	
	ВалютыКошельков = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Кошельки, "Валюта");
	
	Для каждого СтрокаТаблицы из Объект.Перемещения Цикл
		СтрокаТаблицы.ВалютаКошелькаКуда = ВалютыКошельков.Получить(СтрокаТаблицы.КошелекКуда);
		СтрокаТаблицы.ВалютаКошелькаОткуда = ВалютыКошельков.Получить(СтрокаТаблицы.КошелекОткуда);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти