#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ЗаписатьДанныеСеанса(ДанныеСеанса) Экспорт
	
	Менеджер = РегистрыСведений.мх_ДанныеСеансовИнформационнойБазы.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Менеджер, ДанныеСеанса);
	Менеджер.Записать();
	
КонецПроцедуры

Функция ДанныеСеанса(НомерСеанса, Данные = "") Экспорт
	
	Если ПустаяСтрока(Данные) Тогда
		Ключи = ИменаПолейДанныхСеанса();
	Иначе
		Ключи = Данные;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура(Ключи);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НомерСеанса", НомерСеанса);
	Запрос.Текст = 
	"ВЫБРАТЬ *
	|ИЗ
	|	РегистрСведений.мх_ДанныеСеансовИнформационнойБазы КАК ДанныеСеансовИнформационнойБазы
	|ГДЕ
	|	ДанныеСеансовИнформационнойБазы.НомерСеанса = &НомерСеанса";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ИменаПолейДанныхСеанса()
	
	ИменаПолей = Новый Массив;
	
	Для каждого Ресурс из Метаданные.РегистрыСведений.мх_ДанныеСеансовИнформационнойБазы.Ресурсы Цикл
		ИменаПолей.Добавить(Ресурс.Имя);
	КонецЦикла;
	
	СтрокаИмен = СтрСоединить(ИменаПолей, ",");
	
	Возврат СтрокаИмен;
	
КонецФункции

#КонецОбласти

#КонецЕсли
