#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПланированиеБюджета = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеБюджета");
	СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	УстановитьОформлениеДляМобильногоКлиента();
	
	Обработки.РабочийСтол.ЗаполнитьТаблицуБлоков(ТаблицаБлоков);
	
	ИнициализироватьДанныеРабочегоСтола();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьРабочийСтол" Тогда
		ОбновитьОтображениеНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расходы_ПериодНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	Оповещение = Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтотОбъект);

	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = Объект.ПериодОтчета;
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура БалансБюджета_БалансНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("ПланированиеБюджета", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.ПланированиеБюджета.Форма", ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение(Команда)

	ОбновитьОтображениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Расходы_СуммаЗаПериодНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("АнализРасходов", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.АнализРасходов.Форма", ПараметрыОтчета);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	ОткрытьФормуДобавленияРасхода(Неопределено);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ОстаткиКошельковНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТипЧисло = Новый ОписаниеТипов("Число");
	
	Префикс = "ОстаткиКошельков_Кошелек";
	НомерКошелька = СтрЗаменить(Элемент.Имя, Префикс, "");
	НомерКошелька = ТипЧисло.ПривестиЗначение(НомерКошелька);
	
	ДанныеКошелька = ОстаткиКошельков_ДанныеОстатков[НомерКошелька];
	
	ОткрытьФормуДобавленияРасхода(ДанныеКошелька.Кошелек);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Оформление

&НаСервере
Процедура УстановитьОформлениеДляМобильногоКлиента()
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	// включаем разделители контейнеров
	Элементы.ДекорацияРазделитель1.Видимость = Истина;
	Элементы.ДекорацияРазделитель2.Видимость = Истина;
	
	// строки контейнеров группируем вертикально
	
	СтрокиКонтейнеров = Новый Массив;
	СтрокиКонтейнеров.Добавить("СтрокаКонтейнеров0");
	СтрокиКонтейнеров.Добавить("СтрокаКонтейнеров1");
	СтрокиКонтейнеров.Добавить("СтрокаКонтейнеров2");
	
	Контейнеры = Новый Массив;
	
	Для каждого Строка из СтрокиКонтейнеров Цикл
		
		Элемент = Элементы[Строка];
		Элемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
		Для каждого ПодчиненныйЭлемент из Элемент.ПодчиненныеЭлементы Цикл
			Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
				Контейнеры.Добавить(ПодчиненныйЭлемент.Имя);
			КонецЕсли;
		КонецЦикла;
				
	КонецЦикла;
	
	// подчиненные элементы контейнеров растягиваем по горизонтали
	
	Для каждого ИмяКонтейнера из Контейнеры Цикл
		
		Контейнер = Элементы[ИмяКонтейнера];
		
		Для каждого Группа из Контейнер.ПодчиненныеЭлементы Цикл
			
			Группа.РастягиватьПоГоризонтали = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Кошельки

&НаСервере
Процедура ВывестиНаФормуОстаткиПоКошелькам()

	ГруппаРодитель = Элементы.ГруппаОстаткиКошельковОтступ;
	Шрифт = Новый Шрифт(, 10, Ложь, , , , );
	
	// Удалить лишние элементы, если требуется
	
	ВсегоЭлементов = 0;
	Для каждого Элемент из ГруппаРодитель.ПодчиненныеЭлементы Цикл
		Если СтрНачинаетсяС(Элемент.Имя, "ОстаткиКошельков_Кошелек") Тогда
			ВсегоЭлементов = ВсегоЭлементов + 1;
		КонецЕсли;
	КонецЦикла;
	
	ВсегоКошельков = ОстаткиКошельков_ДанныеОстатков.Количество();
	
	Разница = ВсегоКошельков - ВсегоЭлементов;
	
	// Если кошельков больше, чем элементов, тогда
	// добавим недостающие
	Если Разница > 0 Тогда
		
		Для сч = ВсегоЭлементов по ВсегоКошельков - 1 Цикл
			
			ИмяЭлемента					 = "ОстаткиКошельков_Кошелек" + сч;
			ЭлементФормы				 = ЭтотОбъект.Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), ГруппаРодитель);
			ЭлементФормы.Гиперссылка	 = Истина;
			ЭлементФормы.ШрифтЗаголовка	 = Новый Шрифт(,12);
			ЭлементФормы.Шрифт			 = Шрифт;
			ЭлементФормы.ПутьКДанным	 = ИмяЭлемента;
			ЭлементФормы.УстановитьДействие("Нажатие", "ОстаткиКошельковНажатие");
			
		КонецЦикла;
		
	// Если кошельков меньше, то удалим лишние элементы
	ИначеЕсли Разница < 0 Тогда
		
		Пока ВсегоЭлементов > ВсегоКошельков Цикл
			
			ИмяЭлемента = "ОстаткиКошельков_Кошелек" + (ВсегоЭлементов - 1);
			Элемент = ЭтотОбъект.Элементы[ИмяЭлемента];
			ЭтотОбъект.Элементы.Удалить(Элемент);
			
			ВсегоЭлементов = ВсегоЭлементов - 1;
			
		КонецЦикла;
		
	КонецЕсли;
		
	
	Сч = 0;
	
	Для каждого ДанныеОстатка из ОстаткиКошельков_ДанныеОстатков Цикл
		
		ИмяЭлемента = "ОстаткиКошельков_Кошелек" + сч;
		
		ПредставлениеСуммы = мх_РабочийСтол.ПредставлениеСуммы(ДанныеОстатка.Баланс, "", Истина);
		
		ЭтотОбъект[ИмяЭлемента] = ПредставлениеСуммы;
		
		ЭлементФормы				 = ЭтотОбъект.Элементы[ИмяЭлемента];		
		ЭлементФормы.Заголовок		 = Строка(ДанныеОстатка.Кошелек);
		
		Сч = Сч + 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыборПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПериодОтчета = Результат;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДанныеРабочегоСтола()
	
	КлючОбъекта = "Обработка.РабочийСтол.Форма.Форма/ТекущиеДанные";
	
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
	
	Если Настройки = Неопределено Тогда
		Настройки = Новый Соответствие;
		Настройки.Вставить("Объект.ПериодОтчета", Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц));
		ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта,,Настройки);
	КонецЕсли;
	
	Объект.ПериодОтчета = Настройки.Получить("Объект.ПериодОтчета");
	
	ПрочитатьДанныеБлоков();
	
	ВывестиНаФормуОстаткиПоКошелькам();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияРасхода(Кошелек)
	
	ПараметрыФормы = Новый Структура("Кошелек", Кошелек);
	
	ОткрытьФорму("Обработка.РабочийСтол.Форма.ДобавлениеРасхода", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеНаСервере()
	
	ПрочитатьДанныеБлоков();
	
	ВывестиНаФормуОстаткиПоКошелькам();
	
КонецПроцедуры

#Область Блоки

Процедура ПрочитатьДанныеБлоков()
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("УникальныйИдентификатор",          УникальныйИдентификатор);
	СтруктураПараметров.Вставить("АдресХранилища",                   АдресХранилища);
	СтруктураПараметров.Вставить("ПериодОтчета",                     Объект.ПериодОтчета);
	
	ПараметрыПроцедуры = Новый Массив();
	ПараметрыПроцедуры.Добавить(СтруктураПараметров);

	Для Каждого Блок Из ТаблицаБлоков Цикл
		
//		Если Не Блок.Пометка Тогда
//			Продолжить;
//		КонецЕсли;
		
		Если Не ПустаяСтрока(Блок.ПроцедураПолученияДанных) Тогда
			
			ОбщегоНазначения.ВыполнитьМетодКонфигурации(Блок.ПроцедураПолученияДанных, ПараметрыПроцедуры);
			
			ДанныеБлока = ПолучитьИзВременногоХранилища(АдресХранилища);
			Если ТипЗнч(ДанныеБлока) = Тип("Структура") Тогда
				
				ТаблицаОстатков = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеБлока, "ОстаткиКошельков_ДанныеОстатков", Неопределено);
				
				Если ТаблицаОстатков <> Неопределено Тогда
					СписокИсключений = "ОстаткиКошельков_ДанныеОстатков";
					ОстаткиКошельков_ДанныеОстатков.Загрузить(ТаблицаОстатков);
				Иначе
					СписокИсключений = "";
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеБлока,, СписокИсключений);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;	
		
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		УдалитьИзВременногоХранилища(АдресХранилища);
		АдресХранилища = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти