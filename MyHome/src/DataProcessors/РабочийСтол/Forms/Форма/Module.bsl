#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПланированиеБюджета = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеБюджета");
	СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
		
	Если Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаНачала) И Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаОкончания) Тогда
		Объект.ПериодОтчета = Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц);
	КонецЕсли;

	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(Объект.ПериодОтчета.ДатаНачала, Объект.ПериодОтчета.ДатаОкончания);

	НовыйРасходДата = ТекущаяДата();
	
	УстановитьОформление();
	ВывестиНаФормуОстаткиПоКошелькам();
	РассчитатьБалансБюджета();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьОформлениеНаКлиенте();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НовыйРасходСтатьяРасходовПриИзменении(Элемент)
	НовыйРасходСтатьяРасходовПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НовыйРасходСтатьяРасходовПриИзмененииНаСервере()
	СобытияФормВызовСервера.УстановитьОграничениеТипаДляАналитикиДоходовРасходов(Элементы.НовыйРасходАналитика, НовыйРасходСтатьяРасходов);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СканироватьЧек(Команда)

	Оповещение = Новый ОписаниеОповещения("СканироватьШтрихкодЗавершение", ЭтотОбъект);

	СканированиеКлиент.СканироватьШтрихкод(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПериодНажатие(Элемент)

	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = Объект.ПериодОтчета;
	Если Диалог.Редактировать() Тогда
		Объект.ПериодОтчета = Диалог.Период;
	КонецЕсли;

	ОбновитьОтображениеНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура НадписьБалансБюджетаНажатие(Элемент)
		
	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("ПланированиеБюджета", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.ПланированиеБюджета.Форма", ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение(Команда)
	ОбновитьОтображениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СуммаРасходаЗаПериодНажатие(Элемент)

	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("АнализРасходов", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.АнализРасходов.Форма", ПараметрыОтчета);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)

	Если Не РеквизитыРасходаЗаполнены() Тогда
		Возврат;
	КонецЕсли;

	ПараметрыРасхода = НовыеПараметрыРасхода();

	РасходУжеСуществует = РасходУжеСуществует(ПараметрыРасхода);

	Если РасходУжеСуществует Тогда
		ТекстВопроса = Нстр("ru = 'В системе уже существуют данные о расходе с такими параметрами.
							|Добавить еще раз?'");
		ПараметрыОповещения = Новый Структура("ПараметрыРасхода", ПараметрыРасхода);
		Оповещение = Новый ОписаниеОповещения("ДобавитьРасходПослеВопроса", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

	Иначе
		ДобавитьРасходНаКлиенте(ПараметрыРасхода);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДобавлениеРасхода

&НаКлиенте
Функция РеквизитыРасходаЗаполнены()

	РеквизитыЗаполнены = Истина;

	Если Не ЗначениеЗаполнено(НовыйРасходКошелек) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан кошелек", , "НовыйРасходКошелек", , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(НовыйРасходДата) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана дата", , "НовыйРасходДата", , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(НовыйРасходСтатьяРасходов) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана статья расходов", , "НовыйРасходСтатьяРасходов", , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(НовыйРасходСумма) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана сумма", , "НовыйРасходСумма", , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;

	Возврат РеквизитыЗаполнены;

КонецФункции

&НаКлиенте
Процедура ДобавитьРасходПослеВопроса(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ДобавитьРасходНаКлиенте(ДопПараметры.ПараметрыРасхода);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасходНаКлиенте(ПараметрыРасхода)

	Результат = ДобавитьРасходНаСервере(ПараметрыРасхода);

	Если Результат.Успех Тогда
		ПоказатьОповещениеПользователя("Расход добавлен");
	Иначе
		ВызватьИсключение Результат.ОписаниеОшибки;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьРасходНаСервере(ПараметрыРасхода)

	Возврат Документы.РегистрацияРасходов.ДобавитьРасход(ПараметрыРасхода);

КонецФункции

&НаСервереБезКонтекста
Функция РасходУжеСуществует(ПараметрыРасхода)

	Возврат Документы.РегистрацияРасходов.РасходСуществует(ПараметрыРасхода);

КонецФункции

&НаКлиенте
Функция НовыеПараметрыРасхода()

	ПараметрыРасхода = Новый Структура;
	ПараметрыРасхода.Вставить("Кошелек", НовыйРасходКошелек);
	ПараметрыРасхода.Вставить("СтатьяРасходов", НовыйРасходСтатьяРасходов);
	ПараметрыРасхода.Вставить("АналитикаРасходов", НовыйРасходАналитика);
	ПараметрыРасхода.Вставить("Сумма", НовыйРасходСумма);
	ПараметрыРасхода.Вставить("ДатаОперации", НовыйРасходДата);
	ПараметрыРасхода.Вставить("Регистратор", Неопределено);

	Возврат ПараметрыРасхода;

КонецФункции

#КонецОбласти

#Область Оформление

&НаКлиенте
Процедура УстановитьОформлениеНаКлиенте()

#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда
	Элементы.ГруппаКолонки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.СканироватьЧек.Видимость = Истина;
#Иначе
		Элементы.ГруппаКолонки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		Элементы.СканироватьЧек.Видимость = Ложь;
#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеПериода()

	Если Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаНачала) И Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаОкончания) Тогда
		Элементы.ДекорацияПериод.Заголовок = НСтр("ru = 'Всё время'");
	Иначе
		Элементы.ДекорацияПериод.Заголовок = Строка(Объект.ПериодОтчета);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеНадписиСуммыРасхода()

	СуммаРасходаСтрокой = Формат(ВсегоРасходовЗаПериод, "ЧДЦ=0; ЧН=0; ЧГ=3,0;");
	СуммаРасходаСтрокой = СуммаРасходаСтрокой + " руб.";

	Элементы.СуммаРасходаЗаПериод.Заголовок = СуммаРасходаСтрокой;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеБалансаБюджета()
	
	Если ИспользоватьПланированиеБюджета = Истина Тогда
		Элементы.ГруппаКолонка2.Видимость = Истина;
		Элементы.ДекорацияНадписьРазделитель2.Видимость = Истина;
	Иначе
		Элементы.ГруппаКолонка2.Видимость = Ложь;
		Элементы.ДекорацияНадписьРазделитель2.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформление()

	УстановитьПредставлениеПериода();
	УстановитьОформлениеНадписиСуммыРасхода();
	УстановитьОформлениеБалансаБюджета()

КонецПроцедуры

#КонецОбласти

#Область Кошельки

&НаСервереБезКонтекста
Функция ОстаткиПоКошелькам()

	Кошельки = Справочники.КошелькиИСчета.КошелькиДляОтображенияНаРабочемСтоле();
	Остатки = Справочники.КошелькиИСчета.ОстаткиКошельков(Кошельки);

	Возврат Остатки;

КонецФункции

&НаСервере
Процедура РассчитатьБалансБюджета()
	
	Если НЕ ИспользоватьПланированиеБюджета Тогда
		Элементы.ГруппаКолонка2.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ГруппаКолонка2.Видимость = Истина;
	КонецЕсли;
	
	ДатаПроверки = Мин(ТекущаяДата(), Объект.ПериодОтчета.ДатаОкончания);
	СуммаБаланса = ПланированиеБюджетаСервер.ОстатокБюджетаНаДату(ДатаПроверки);
	
	Элементы.НадписьБалансБюджета.Заголовок = Строка(СуммаБаланса) + " руб.";
	
	ШаблонЗаголовкаБаланса = НСтр("ru = 'Баланс бюджета (%1)'");
	Элементы.Колонка2Заголовок.Заголовок = СтрШаблон(ШаблонЗаголовкаБаланса, Формат(ДатаПроверки, "ДФ=dd.MM.yyyy;"));
	
	Если СуммаБаланса < 0 Тогда
		Элементы.НадписьБалансБюджета.ЦветТекста = Метаданные.ЭлементыСтиля.ЦветОтрицательнаяСумма.Значение;
	Иначе
		Элементы.НадписьБалансБюджета.ЦветТекста = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ВывестиНаФормуОстаткиПоКошелькам()

	УдаляемыеЭлементы = Новый Массив;

	Для Каждого УдаляемыйЭлемент Из Элементы.ГруппаОстаткиКошельки.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;

	Для Каждого УдаляемыйЭлемент Из Элементы.ГруппаОстаткиСуммы.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;

	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;

	Остатки = ОстаткиПоКошелькам();

	ГруппаКошельки = Элементы.ГруппаОстаткиКошельки;
	ГруппаСуммы = Элементы.ГруппаОстаткиСуммы;

	сч = 1;

	Для Каждого ДанныеОстатка Из Остатки Цикл

		НадписьКошелек = Элементы.Добавить("Кошелек_" + сч, Тип("ДекорацияФормы"), ГруппаКошельки);
		НадписьКошелек.Заголовок = Строка(ДанныеОстатка.Ключ);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьКошелек);

		НадписьСумма = Элементы.Добавить("Сумма_" + сч, Тип("ДекорацияФормы"), ГруппаСуммы);
		НадписьСумма.Заголовок = Строка(ДанныеОстатка.Значение);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьСумма);
		УстановитьОформлениеЭлементаДекорацииСумма(НадписьСумма, ДанныеОстатка.Значение);

		сч = сч + 1;

	КонецЦикла;
	
	Если Остатки.Количество() = 0 Тогда
		Элементы.ГруппаКолонка3.Видимость = Ложь;
	Иначе
		Элементы.ГруппаКолонка3.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииКошелька(Элемент)

	Элемент.Шрифт = Новый Шрифт(, 10, Истина, , , , );
	Элемент.ЦветФона = Метаданные.ЭлементыСтиля.ЦветФонаГруппыИндикатора.Значение;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииСумма(Элемент, Значение)

	ЦветОтрицательный = Метаданные.ЭлементыСтиля.ЦветОтрицательнаяСумма.Значение;
	ЦветПоложительный =	Метаданные.ЭлементыСтиля.ЦветПоложительнаяСумма.Значение;

	Если Значение > 0 Тогда
		Элемент.ЦветТекста = ЦветПоложительный;
	КонецЕсли;

	Если Значение < 0 Тогда
		Элемент.ЦветТекста = ЦветОтрицательный;
	КонецЕсли;
	
	Элемент.ЦветФона = Метаданные.ЭлементыСтиля.ЦветФонаГруппыИндикатора.Значение;

КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция СуммаРасходаЗаПериод(ДатаНачала, ДатаОкончания)

	СуммаРасхода = 0;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.Расходы.Обороты(&ДатаНачала, &ДатаОкончания,,) КАК РасходыОбороты";

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		СуммаРасхода = Выборка.СуммаОборот;
	КонецЕсли;

	Возврат СуммаРасхода;

КонецФункции

&НаСервере
Процедура ОбновитьОтображениеНаСервере()

	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(Объект.ПериодОтчета.ДатаНачала, Объект.ПериодОтчета.ДатаОкончания);
	УстановитьОформлениеНадписиСуммыРасхода();
	ВывестиНаФормуОстаткиПоКошелькам();
	РассчитатьБалансБюджета();
	УстановитьПредставлениеПериода();

КонецПроцедуры

&НаКлиенте
Процедура СканироватьШтрихкодЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Успех = Ложь Тогда
		Возврат;
	КонецЕсли;

	РезультатЧтения = ИнтеграцияСВнешнимиСистемамиКлиентСервер.ПрочитатьДанныеШтрихкодаЧека(Результат.Штрихкод);

	Если Не РезультатЧтения.Успех Тогда
		ПоказатьПредупреждение( , РезультатЧтения.ОписаниеОшибки);
		Возврат;
	КонецЕсли;

	ДанныеЧека = ИнтеграцияСВнешнимиСистемамиКлиентСервер.ДанныеЧека(РезультатЧтения);

	НовыйРасходСумма = ДанныеЧека.Сумма;
	НовыйРасходДата = ДанныеЧека.Дата;
КонецПроцедуры

#КонецОбласти