#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЦветФонаГруппыИндикатора = ЦветаСтиля.ЦветФонаГруппыИндикатора;
	ЦветОтрицательнаяСумма = ЦветаСтиля.ЦветОтрицательнаяСумма;
	ЦветПоложительнаяСумма = ЦветаСтиля.ЦветПоложительнаяСумма;
	
	ИспользоватьПланированиеБюджета = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеБюджета");
	СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);
	
	ИнициализироватьДанныеРабочегоСтола();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьРабочийСтол" Тогда
		ОбновитьОтображениеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПериодНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтотОбъект);

	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = Объект.ПериодОтчета;
	Диалог.Показать(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура НадписьБалансБюджетаНажатие(Элемент)
		
	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("ПланированиеБюджета", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.ПланированиеБюджета.Форма", ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение(Команда)
	ОбновитьОтображениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СуммаРасходаЗаПериодНажатие(Элемент)

	ПараметрыДанных = Новый Структура("ПериодОтчета", Объект.ПериодОтчета);
	//@skip-warning
	ПользовательскиеНастройки = СКДВызовСервера.УстановитьНастройкиОтчета("АнализРасходов", ПараметрыДанных, Неопределено);

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

	ОткрытьФорму("Отчет.АнализРасходов.Форма", ПараметрыОтчета);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	ОткрытьФормуДобавленияРасхода(Неопределено);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ДобавитьРасходПоКошельку(Команда)

	ТекущийЭлемент = ЭтотОбъект.ТекущийЭлемент;
	
	Если ТипЗнч(ТекущийЭлемент) <> Тип("КнопкаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторКошелька = СтрЗаменить(ТекущийЭлемент.Имя, "ДобавитьРасходКошелек", "");
	ИдентификаторКошелька = СтрЗаменить(ИдентификаторКошелька, "_",  "-");
	
	Кошелек = КошелекПоИдентификатору(ИдентификаторКошелька);
	
	ОткрытьФормуДобавленияРасхода(Кошелек);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Оформление

&НаСервере
Процедура УстановитьПредставлениеПериода()

	Если Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаНачала) И Не ЗначениеЗаполнено(Объект.ПериодОтчета.ДатаОкончания) Тогда
		Элементы.ДекорацияПериод.Заголовок = НСтр("ru = 'Всё время'");
	Иначе
		Элементы.ДекорацияПериод.Заголовок = Строка(Объект.ПериодОтчета);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеНадписиСуммыРасхода()

	СуммаРасходаСтрокой = Формат(ВсегоРасходовЗаПериод, "ЧДЦ=0; ЧН=0; ЧГ=3,0;");
	СуммаРасходаСтрокой = СуммаРасходаСтрокой + " руб.";

	Элементы.СуммаРасходаЗаПериод.Заголовок = СуммаРасходаСтрокой;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеБалансаБюджета()
	
	Если ИспользоватьПланированиеБюджета = Истина Тогда
		Элементы.ГруппаКолонка2.Видимость = Истина;
		Элементы.ДекорацияНадписьРазделитель2.Видимость = Истина;
	Иначе
		Элементы.ГруппаКолонка2.Видимость = Ложь;
		Элементы.ДекорацияНадписьРазделитель2.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформление()

	УстановитьПредставлениеПериода();
	УстановитьОформлениеНадписиСуммыРасхода();
	УстановитьОформлениеБалансаБюджета()

КонецПроцедуры

#КонецОбласти

#Область Кошельки

&НаСервереБезКонтекста
Функция КошелекПоИдентификатору(ИдентификаторКошелька)
	
	СсылкаНаКошелек = ПредопределенноеЗначение("Справочник.КошелькиИСчета.ПустаяСсылка");
	
	Попытка		
		УникальныйИдентификатор = Новый УникальныйИдентификатор(ИдентификаторКошелька);		
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	СсылкаНаКошелек = Справочники.КошелькиИСчета.ПолучитьСсылку(УникальныйИдентификатор);
	
	Если НЕ ОбщегоНазначения.СсылкаСуществует(СсылкаНаКошелек) Тогда
		СсылкаНаКошелек = Неопределено;
	КонецЕсли;
	
	Возврат СсылкаНаКошелек;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОстаткиПоКошелькам()

	Кошельки = Справочники.КошелькиИСчета.КошелькиДляОтображенияНаРабочемСтоле();
	Остатки = Справочники.КошелькиИСчета.ОстаткиКошельков(Кошельки);

	Возврат Остатки;

КонецФункции

&НаСервере
Процедура РассчитатьБалансБюджета()
	
	Если НЕ ИспользоватьПланированиеБюджета Тогда
		Элементы.ГруппаКолонка2.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ГруппаКолонка2.Видимость = Истина;
	КонецЕсли;
	
	ДатаПроверки = Мин(ТекущаяДата(), Объект.ПериодОтчета.ДатаОкончания);
	СуммаБаланса = ПланированиеБюджетаСервер.ОстатокБюджетаНаДату(ДатаПроверки);
	
	Элементы.НадписьБалансБюджета.Заголовок = Строка(СуммаБаланса) + " руб.";
	
	ШаблонЗаголовкаБаланса = НСтр("ru = 'Баланс бюджета (%1)'");
	Элементы.Колонка2Заголовок.Заголовок = СтрШаблон(ШаблонЗаголовкаБаланса, Формат(ДатаПроверки, "ДФ=dd.MM.yyyy;"));
	
	Если СуммаБаланса < 0 Тогда
		Элементы.НадписьБалансБюджета.ЦветТекста = Метаданные.ЭлементыСтиля.ЦветОтрицательнаяСумма.Значение;
	Иначе
		Элементы.НадписьБалансБюджета.ЦветТекста = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНаФормуОстаткиПоКошелькам()

	УдаляемыеЭлементы = Новый Массив;

	Для Каждого УдаляемыйЭлемент Из Элементы.ГруппаОстаткиКошельки.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;

	Для Каждого УдаляемыйЭлемент Из Элементы.ГруппаОстаткиСуммы.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;
	
	Для Каждого УдаляемыйЭлемент Из Элементы.ГруппаОстаткиКнопкиДобавитьРасход.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;

	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;

	Остатки = ОстаткиПоКошелькам();

	ГруппаКошельки = Элементы.ГруппаОстаткиКошельки;
	ГруппаСуммы = Элементы.ГруппаОстаткиСуммы;
	ГруппаКнопки = Элементы.ГруппаОстаткиКнопкиДобавитьРасход;

	КомандаДобавитьРасход = ЭтаФорма.Команды.Найти("ДобавитьРасходПоКошельку");
	
	Если КомандаДобавитьРасход = Неопределено Тогда
		КомандаДобавитьРасход = ЭтотОбъект.Команды.Добавить("ДобавитьРасходПоКошельку");
		КомандаДобавитьРасход.Действие = "ДобавитьРасходПоКошельку";
	КонецЕсли;
	
	сч = 1;

	Для Каждого ДанныеОстатка Из Остатки Цикл

		НадписьКошелек = Элементы.Добавить("Кошелек_" + сч, Тип("ДекорацияФормы"), ГруппаКошельки);
		НадписьКошелек.Заголовок = Строка(ДанныеОстатка.Ключ);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьКошелек);

		НадписьСумма = Элементы.Добавить("Сумма_" + сч, Тип("ДекорацияФормы"), ГруппаСуммы);
		НадписьСумма.Заголовок = Строка(ДанныеОстатка.Значение);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьСумма);
		УстановитьОформлениеЭлементаДекорацииСумма(НадписьСумма, ДанныеОстатка.Значение);
		
		ДобавитьКнопкуРасходаПоКошельку(ДанныеОстатка.Ключ, ГруппаКнопки);

		сч = сч + 1;

	КонецЦикла;
	
	Если Остатки.Количество() = 0 Тогда
		Элементы.ГруппаКолонка3.Видимость = Ложь;
	Иначе
		Элементы.ГруппаКолонка3.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкуРасходаПоКошельку(Кошелек, ГруппаКнопок)
	
	ИдентификаторКошелька = Строка(Кошелек.УникальныйИдентификатор());
	УникальноеИмяЭлемента = СтрЗаменить(ИдентификаторКошелька, "-", "_");
	УникальноеИмяЭлемента = ВРЕГ(УникальноеИмяЭлемента);
	
	КнопкаДобавитьРасход						 = Элементы.Добавить("ДобавитьРасходКошелек" + УникальноеИмяЭлемента, Тип("КнопкаФормы"), ГруппаКнопок);
	КнопкаДобавитьРасход.Отображение			 = ОтображениеКнопки.Картинка;
	КнопкаДобавитьРасход.Картинка				 = БиблиотекаКартинок.ДобавитьРасход;
	КнопкаДобавитьРасход.ИмяКоманды				 = "ДобавитьРасходПоКошельку";
	КнопкаДобавитьРасход.ЦветФона				 = ЦветаСтиля.ЦветФонаГруппыИндикатора;
	КнопкаДобавитьРасход.ОтображениеФигуры		 = ОтображениеФигурыКнопки.ПриАктивности;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииКошелька(Элемент)

	Элемент.Шрифт = Новый Шрифт(, 10, Истина, , , , );
	Элемент.ЦветФона = ЦветФонаГруппыИндикатора;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииСумма(Элемент, Значение)

	Если Значение > 0 Тогда
		Элемент.ЦветТекста = ЦветПоложительнаяСумма;
	КонецЕсли;

	Если Значение < 0 Тогда
		Элемент.ЦветТекста = ЦветОтрицательнаяСумма;
	КонецЕсли;
	
	Элемент.ЦветФона = ЦветФонаГруппыИндикатора

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыборПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПериодОтчета = Результат;
	ОбновитьОтображениеНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СуммаРасходаЗаПериод(ДатаНачала, ДатаОкончания)

	СуммаРасхода = 0;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.Расходы.Обороты(&ДатаНачала, &ДатаОкончания,,) КАК РасходыОбороты";

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		СуммаРасхода = Выборка.СуммаОборот;
	КонецЕсли;

	Возврат СуммаРасхода;

КонецФункции

&НаСервере
Процедура ОбновитьОтображениеНаСервере()

	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(Объект.ПериодОтчета.ДатаНачала, Объект.ПериодОтчета.ДатаОкончания);
	УстановитьОформлениеНадписиСуммыРасхода();
	ВывестиНаФормуОстаткиПоКошелькам();
	РассчитатьБалансБюджета();
	УстановитьПредставлениеПериода();

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДанныеРабочегоСтола()
	
	КлючОбъекта = "Обработка.РабочийСтол.Форма.Форма/ТекущиеДанные";
	
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
	
	Если Настройки = Неопределено Тогда
		Настройки = Новый Соответствие;
		Настройки.Вставить("Объект.ПериодОтчета", Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц));
		ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта,,Настройки);
	КонецЕсли;
	
	Объект.ПериодОтчета = Настройки.Получить("Объект.ПериодОтчета");

	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(Объект.ПериодОтчета.ДатаНачала, Объект.ПериодОтчета.ДатаОкончания);
	
	УстановитьОформление();
	ВывестиНаФормуОстаткиПоКошелькам();
	РассчитатьБалансБюджета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияРасхода(Кошелек)
	
	ПараметрыФормы = Новый Структура("Кошелек", Кошелек);
	
	ОткрытьФорму("Обработка.РабочийСтол.Форма.ДобавлениеРасхода", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

#КонецОбласти