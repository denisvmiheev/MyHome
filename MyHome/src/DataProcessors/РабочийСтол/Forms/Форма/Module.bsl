#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ПериодОтчета.ДатаНачала)
		И НЕ ЗначениеЗаполнено(ПериодОтчета.ДатаОкончания) Тогда
			
		ПериодОтчета = Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц);
		
	КонецЕсли;

	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	
	НовыйРасходДата = ТекущаяДата();	
	
	СобытияФорм.ЗаполнитьКэшированныеЗначенияДляСвязиТиповДоходовРасходов(ЭтотОбъект);
	
	УстановитьОформление();
	
	ВывестиНаФормуОстаткиПоКошелькам();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОформлениеНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НовыйРасходСтатьяРасходовПриИзменении(Элемент)
	НовыйРасходСтатьяРасходовПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НовыйРасходСтатьяРасходовПриИзмененииНаСервере()
	СобытияФормВызовСервера.УстановитьОграничениеТипаДляАналитикиДоходовРасходов(Элементы.НовыйРасходАналитика, НовыйРасходСтатьяРасходов);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СканироватьЧек(Команда)
	
	Оповещение = Новый ОписаниеОповещения("СканироватьШтрихкодЗавершение", ЭтотОбъект);
	
	СканированиеКлиент.СканироватьШтрихкод(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПериодНажатие(Элемент)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ПериодОтчета;
	Если Диалог.Редактировать() Тогда 
    	ПериодОтчета = Диалог.Период;
	КонецЕсли;
	
	ОбновитьОтображениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение(Команда)
	ОбновитьОтображениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СуммаРасходаЗаПериодНажатие(Элемент)
	
	ПараметрыДанных = Новый Структура("ПериодОтчета", ПериодОтчета);
	ПользовательскиеНастройки = УстановитьНастройкиОтчета("АнализРасходов", ПараметрыДанных, Неопределено);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	ОткрытьФорму("Отчет.АнализРасходов.Форма", ПараметрыОтчета);	

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасход(Команда)
	
	Если НЕ РеквизитыРасходаЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРасхода = НовыеПараметрыРасхода();
	
	РасходУжеСуществует = РасходУжеСуществует(ПараметрыРасхода);
	
	Если РасходУжеСуществует Тогда
		ТекстВопроса = Нстр("ru = 'В системе уже существуют данные о расходе с такими параметрами.
					   |Добавить еще раз?'");
		ПараметрыОповещения = Новый Структура("ПараметрыРасхода", ПараметрыРасхода);
		Оповещение = Новый ОписаниеОповещения("ДобавитьРасходПослеВопроса", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
	Иначе
		ДобавитьРасходНаКлиенте(ПараметрыРасхода);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДобавлениеРасхода

&НаКлиенте
Функция РеквизитыРасходаЗаполнены()
	
	РеквизитыЗаполнены = Истина;
	
	Если НЕ ЗначениеЗаполнено(НовыйРасходКошелек) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан кошелек", , "НовыйРасходКошелек" , , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НовыйРасходДата) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана дата", , "НовыйРасходДата" , , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(НовыйРасходСтатьяРасходов) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана статья расходов", , "НовыйРасходСтатьяРасходов" , , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;
			
	Если НЕ ЗначениеЗаполнено(НовыйРасходСумма) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана сумма", , "НовыйРасходСумма" , , );
		РеквизитыЗаполнены = Ложь;
	КонецЕсли;
	
	Возврат РеквизитыЗаполнены;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьРасходПослеВопроса(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьРасходНаКлиенте(ДопПараметры.ПараметрыРасхода);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРасходНаКлиенте(ПараметрыРасхода)
	
	Результат = ДобавитьРасходНаСервере(ПараметрыРасхода);
	
	Если Результат.Успех Тогда
		ПоказатьОповещениеПользователя("Расход добавлен");
	Иначе
		ВызватьИсключение Результат.ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьРасходНаСервере(ПараметрыРасхода)
	
	Возврат Документы.РегистрацияРасходов.ДобавитьРасход(ПараметрыРасхода);
	
КонецФункции

&НаСервереБезКонтекста
Функция РасходУжеСуществует(ПараметрыРасхода)
	
	Возврат Документы.РегистрацияРасходов.РасходСуществует(ПараметрыРасхода);
	
КонецФункции

&НаКлиенте
Функция НовыеПараметрыРасхода()
	
	ПараметрыРасхода = Новый Структура;
	ПараметрыРасхода.Вставить("Кошелек"				, НовыйРасходКошелек);
	ПараметрыРасхода.Вставить("СтатьяРасходов"		, НовыйРасходСтатьяРасходов);
	ПараметрыРасхода.Вставить("АналитикаРасходов"	, НовыйРасходАналитика);
	ПараметрыРасхода.Вставить("Сумма"				, НовыйРасходСумма);
	ПараметрыРасхода.Вставить("ДатаОперации"		, НовыйРасходДата);
	ПараметрыРасхода.Вставить("Регистратор"			, Неопределено);
	
	Возврат ПараметрыРасхода;
	
КонецФункции

#КонецОбласти

#Область Оформление

&НаКлиенте
Процедура УстановитьОформлениеНаКлиенте()
	
	#Если МобильноеПриложениеКлиент ИЛИ МобильныйКлиент Тогда
	Элементы.ГруппаКолонки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.СканироватьЧек.Видимость = Истина;
	#Иначе
	Элементы.ГруппаКолонки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.СканироватьЧек.Видимость = Ложь;
	#КонецЕсли
		
КонецПроцедуры


&НаСервере
Процедура УстановитьПредставлениеПериода()
	
	Если НЕ ЗначениеЗаполнено(ПериодОтчета.ДатаНачала)
		И НЕ ЗначениеЗаполнено(ПериодОтчета.ДатаОкончания) Тогда
		
		Элементы.ДекорацияПериод.Заголовок = НСтр("ru = 'Всё время'");;
			
	Иначе			
		Элементы.ДекорацияПериод.Заголовок = Строка(ПериодОтчета);			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеНадписиСуммыРасхода()
	
	СуммаРасходаСтрокой = Формат(ВсегоРасходовЗаПериод, "ЧДЦ=0; ЧН=0; ЧГ=3,0;");
	СуммаРасходаСтрокой = СуммаРасходаСтрокой + " руб.";
	
	Элементы.СуммаРасходаЗаПериод.Заголовок = СуммаРасходаСтрокой;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформление()
	
	УстановитьПредставлениеПериода();
	УстановитьОформлениеНадписиСуммыРасхода();
	
КонецПроцедуры

#КонецОбласти

#Область Кошельки

&НаСервереБезКонтекста
Функция ОстаткиПоКошелькам()
	
	Кошельки = Справочники.КошелькиИСчета.КошелькиДляОтображенияНаРабочемСтоле();
	Остатки = Справочники.КошелькиИСчета.ОстаткиКошельков(Кошельки);
	
	Возврат Остатки;
	
КонецФункции

&НаСервере
Процедура ВывестиНаФормуОстаткиПоКошелькам()
	
	УдаляемыеЭлементы = Новый Массив;
	
	Для каждого УдаляемыйЭлемент из Элементы.ГруппаОстаткиКошельки.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;
	
	Для каждого УдаляемыйЭлемент из Элементы.ГруппаОстаткиСуммы.ПодчиненныеЭлементы Цикл
		УдаляемыеЭлементы.Добавить(УдаляемыйЭлемент);
	КонецЦикла;
	
	Для каждого УдаляемыйЭлемент из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
	Остатки = ОстаткиПоКошелькам();
	
	ГруппаКошельки = Элементы.ГруппаОстаткиКошельки;
	ГруппаСуммы = Элементы.ГруппаОстаткиСуммы;
	
	сч = 1;
	
	Для каждого ДанныеОстатка из Остатки Цикл
		
		НадписьКошелек = Элементы.Добавить("Кошелек_" + сч, Тип("ДекорацияФормы"), ГруппаКошельки);
		НадписьКошелек.Заголовок = Строка(ДанныеОстатка.Ключ);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьКошелек);
		
		НадписьСумма = Элементы.Добавить("Сумма_" + сч, Тип("ДекорацияФормы"), ГруппаСуммы);
		НадписьСумма.Заголовок = Строка(ДанныеОстатка.Значение);
		УстановитьОформлениеЭлементаДекорацииКошелька(НадписьСумма);
		УстановитьОформлениеЭлементаДекорацииСумма(НадписьСумма, ДанныеОстатка.Значение);
		
		сч = сч + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииКошелька(Элемент)
	
	Элемент.Шрифт = Новый Шрифт(, 10, Истина, , , , );
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементаДекорацииСумма(Элемент, Значение)

	ЦветОтрицательный = Метаданные.ЭлементыСтиля.ЦветОтрицательнаяСумма.Значение;
	ЦветПоложительный =	Метаданные.ЭлементыСтиля.ЦветПоложительнаяСумма.Значение;
	
	Если Значение > 0 Тогда
		Элемент.ЦветТекста = ЦветПоложительный;
	КонецЕсли;
	
	Если Значение < 0 Тогда
		Элемент.ЦветТекста = ЦветОтрицательный;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция УстановитьНастройкиОтчета(ИмяОтчета, ПараметрыДанных, Отборы)
	
	Настройки = КомпоновкаДанныхСервер.УстановитьНастройкиОтчета(ИмяОтчета, ПараметрыДанных, Отборы);
	
	Возврат Настройки;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуммаРасходаЗаПериод(ДатаНачала, ДатаОкончания)
	
	СуммаРасхода = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.Расходы.Обороты(&ДатаНачала, &ДатаОкончания,,) КАК РасходыОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СуммаРасхода = Выборка.СуммаОборот;
	КонецЕсли;
	
	Возврат СуммаРасхода;
	
КонецФункции

&НаСервере
Процедура ОбновитьОтображениеНаСервере()
	
	ВсегоРасходовЗаПериод = СуммаРасходаЗаПериод(ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	УстановитьОформлениеНадписиСуммыРасхода();
	ВывестиНаФормуОстаткиПоКошелькам();
	
КонецПроцедуры

&НаКлиенте
Процедура СканироватьШтрихкодЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Успех = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЧтения = ИнтеграцияСВнешнимиСистемамиКлиентСервер.ПрочитатьДанныеШтрихкодаЧека(Результат.Штрихкод);
	
	Если Не РезультатЧтения.Успех Тогда
		ПоказатьПредупреждение(, РезультатЧтения.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ДанныеЧека = ИнтеграцияСВнешнимиСистемамиКлиентСервер.ДанныеЧека(РезультатЧтения);
	
	НовыйРасходСумма = ДанныеЧека.Сумма;
	НовыйРасходДата = ДанныеЧека.Дата;
	
	
КонецПроцедуры

#КонецОбласти