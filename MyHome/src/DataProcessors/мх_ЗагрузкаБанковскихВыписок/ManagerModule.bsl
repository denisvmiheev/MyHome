#Область ПрограммныйИнтерфейс

// Читает файл выписки в таблицу значений
// 
// Параметры:
// 	ПараметрыЧтения - Структура:
// 	 * ВариантЗагрузки - ПеречислениеСсылка.мх_ВариантыЗагрузкиБанковскихВыписок - Вариант загрузки выписки
// 	 * АдресХранилищаФайла - Строка - Адрес файла выписки во временном хранилище
// 	 * ПараметрыФайла - Структура - Параметры файла выписки
// Возвращаемое значение:
//  - ТаблицаЗначений - Прочитанные данные 	
Функция ПрочитатьДанныеФайла(ПараметрыЧтения) Экспорт

	ВариантЗагрузки = ПараметрыЧтения.ВариантЗагрузки;

	Если ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.мх_ВариантыЗагрузкиБанковскихВыписок.Тинькофф") Тогда
		ТаблицаДанныхФайла = ПрочитатьДанныеФайлаВыпискиВТаблицуЗначений(ПараметрыЧтения);
		ТаблицаДанных = ЗаполнитьТаблицуДанныхПоДаннымФайлаТинькофф(ТаблицаДанныхФайла);
	ИначеЕсли ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.мх_ВариантыЗагрузкиБанковскихВыписок.Сбербанк") Тогда
		ТаблицаДанныхФайла = ПрочитатьДанныеФайлаВыпискиВТаблицуЗначений(ПараметрыЧтения);
		ТаблицаДанных = ЗаполнитьТаблицуДанныхПоДаннымФайлаСбербанк(ТаблицаДанныхФайла);
	Иначе
		ВызватьИсключение "Неизвестный вариант выписки";
	КонецЕсли;

	Возврат ТаблицаДанных;

КонецФункции

// Читает файл выписки в таблицу значений
// 
// Параметры:
// 	ПараметрыЧтения - Структура:
// 	 * ВариантЗагрузки - ПеречислениеСсылка.мх_ВариантыЗагрузкиБанковскихВыписок - Вариант загрузки выписки
// 	 * АдресХранилищаФайла - Строка - Адрес файла выписки во временном хранилище
// 	 * ПараметрыФайла - Структура - Параметры файла выписки
// Возвращаемое значение:
//  - ТаблицаЗначений - Прочитанные данные 	
Функция ПрочитатьДанныеФайлаВыпискиВТаблицуЗначений(ПараметрыЧтения)

	ВременныйФайл = ПолучитьИмяВременногоФайла(ПараметрыЧтения.ПараметрыФайла.Расширение);

	ДанныеФайла = ПолучитьИзВременногоХранилища(ПараметрыЧтения.АдресХранилищаФайла);
	ДанныеФайла.Записать(ВременныйФайл);

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ВременныйФайл, СпособЧтенияЗначенийТабличногоДокумента.Значение);

	ОбластьЯчеек = ТабличныйДокумент.Область(1, 1, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);

	ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьЯчеек);

	ПостроительОтчета = Новый ПостроительОтчета;
	ПостроительОтчета.ИсточникДанных = ИсточникДанных;
	ПостроительОтчета.Выполнить();

	ТаблицаДанных = ПостроительОтчета.Результат.Выгрузить();

	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьТаблицуДанныхПоДаннымФайлаТинькофф(ТаблицаФайла)

	Обработка = Обработки.мх_ЗагрузкаБанковскихВыписок.Создать();
	ТаблицаДанных = Обработка.ДанныеВыписки.Выгрузить();

	Для Каждого СтрокаДанных Из ТаблицаФайла Цикл

		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);

		НоваяСтрока.ИдентификаторКарты = СтрокаДанных.НомерКарты;
		НоваяСтрока.МСС = СтрокаДанных.MCC;

	КонецЦикла;

	Возврат ТаблицаДанных;

КонецФункции

Функция ЗаполнитьТаблицуДанныхПоДаннымФайлаСбербанк(ТаблицаФайла)

	Обработка = Обработки.мх_ЗагрузкаБанковскихВыписок.Создать();
	ТаблицаДанных = Обработка.ДанныеВыписки.Выгрузить();

	Для Каждого СтрокаДанных Из ТаблицаФайла Цикл

		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);

		НоваяСтрока.ИдентификаторКарты	 = СтрокаДанных.НомерКарты;
		НоваяСтрока.МСС					 = СтрокаДанных.ТипОперации;
		НоваяСтрока.ДатаОперации		 = СтрокаДанных.ДатаСовершенияОперации;
		НоваяСтрока.СуммаПлатежа		 = СтрокаДанных.СуммаВВалютеСчета;

	КонецЦикла;

	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти